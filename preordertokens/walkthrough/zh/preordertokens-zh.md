# 预售功能设置

Day: Day 13
ID: 13
原文: https://www.notion.so/builder-hub/PreorderTokens-1d45720a23ef80878624e2c98aab1552?source=copy_link
状态: 完成
译者: He Shadow
难度等级: 中级

# **预购代币**

好的——现在我们已经建立了一个有效的 ERC-20 代币合约，是时候升级了。

假设你想**在项目正式启动前发售你的代币**。也许你想筹集一些 ETH，奖励早期信徒，或者制造热度。

这正是许多现实世界加密项目所做的事情——他们进行**代币发售**（也称为预售或 ICO），用户以固定汇率发送 ETH 并获得代币作为回报。

为了实现这一点，我们将构建一个**简单的代币发售合约**，它直接建立在我们刚刚编写的 ERC-20 代币上。但在此之前，我们需要对代币合约进行一项重要的更改，以便其能够正确扩展。

---

## 我们要构建什么

我们即将构建一个**简单但功能强大的代币发售合约**——您可以在预售、早期支持者轮次或发布活动中使用它。

该合约将允许你：

- 以固定的 ETH 价格发售你的自定义 ERC-20 代币
- 设置发售的开始和结束时间
- 强制执行最低和最高购买金额
- 自动处理代币分发
- 防止发售期间的转账（以防止短线抛售或机器人砸盘）
- 完成发售并将筹集的 ETH 转移给项目所有者

因此流程将如下所示：

1. 你（项目方）部署代币发售合约
2. 它（代币发售合约）创建并持有所有代币
3. 买家发送 ETH 并获取代币作为回报
4. 在发售期间，转账功能被**锁定**
5. 发售结束后，你（项目方）完成交易并领取 ETH

很简洁，对吧？

为了使这一切正常工作，我们需要准备好代币以支持继承，并重写一些关键函数。我们先处理这个问题。

---

## 将代币函数标记为可重写

在深入讲解代币发售合约之前，我们需要先对原来的  `SimpleERC20` 合约做一些小调整。

在 Solidity 编程语言里，如果你想在“子合约”里**重写**（重新实现）“父合约”里的某个函数，那父合约里的这个函数必须加上`virtual` 的标记。

所以在你的 `SimpleERC20` 合约里，需要像下面这样把两个函数加上 virtual 标记：

```solidity
function transfer(address _to, uint256 _value) public virtual  returns (bool);
function transferFrom(address _from, address _to, uint256 _value) public virtual  returns (bool);

```

这一变化（指加virtua）会告诉 Solidity：

> “注意，如果有其他合约继承了这个合约，这个函数是可以被重新修改的。”
> 

我们会利用这个特性，在代币发售期间限制代币的转账。

---

## 合约结构解析：`SimplifiedTokenSale`

```solidity
contract SimplifiedTokenSale is SimpleERC20 {

```

这个合约继承自 `SimpleERC20`，因此它拥有我们之前编写的所有 ERC-20 功能——还可以加上我们接下来要写的新功能。

---

## 状态变量 — 发售设置

```solidity
uint256 public tokenPrice;
uint256 public saleStartTime;
uint256 public saleEndTime;
uint256 public minPurchase;
uint256 public maxPurchase;
uint256 public totalRaised;
address public projectOwner;
bool public finalized = false;
bool private initialTransferDone = false;

```

我们逐条解释一下：

- `tokenPrice`：每个代币值多少 ETH（单位是 wei，1 ETH = 10¹⁸ wei）
- `saleStartTime` 和 `saleEndTime`：表示发售开始和结束时间的时间戳
- `minPurchase` 和 `maxPurchase`：单笔交易中允许购买的最小和最大ETH额度
- `totalRaised`：目前为止接收的 ETH总额
- `projectOwner`：发售结束后接收 ETH 的钱包地址
- `finalized`：发售是否已经正式关闭
- `initialTransferDone`：用于确保合约在锁定转账前已收到所有代币

这些变量是整个发售逻辑的基础。

---

## 事件

```solidity
event TokensPurchased(address indexed buyer, uint256 etherAmount, uint256 tokenAmount);
event SaleFinalized(uint256 totalRaised, uint256 totalTokensSold);

```

- `TokensPurchased`：当有人成功购买代币时触发。它会记录购买者、支付的 ETH 数量以及收到的代币数量。
- `SaleFinalized`：发售结束时触发。记录筹集的 ETH 总数和售出的代币数量。

这些事件对前端页面和区块链浏览器来说很有用，可以用来展示发售的实时动态。

---

## 构造函数 — 设置一切

```solidity
constructor(
    uint256 _initialSupply,
    uint256 _tokenPrice,
    uint256 _saleDurationInSeconds,
    uint256 _minPurchase,
    uint256 _maxPurchase,
    address _projectOwner
) SimpleERC20(_initialSupply) {
    tokenPrice = _tokenPrice;
    saleStartTime = block.timestamp;
    saleEndTime = block.timestamp + _saleDurationInSeconds;
    minPurchase = _minPurchase;
    maxPurchase = _maxPurchase;
    projectOwner = _projectOwner;

    // 将所有代币转移至此合约用于发售
    _transfer(msg.sender, address(this), totalSupply);

    // 标记我们已经从部署者那里转移了代币
    initialTransferDone = true;
}

```

让我们逐行分析：

---

```json
constructor(...) SimpleERC20(_initialSupply)

```

这是一个**构造函数**，它会在合约第一次部署时自动执行。

但这里有个重要的事情：这个合约继承自 `SimpleERC20`，所以需要把`_initialSupply` （初始代币数量）传递给父合约，这样才能真的**铸造代币**。

因此，这一行：

```solidity
SimpleERC20(_initialSupply)

```

…… `SimpleERC20`  其实是在后台帮你把全部代币分配给部署者（你）。

---

```json
tokenPrice = _tokenPrice;

```

这句是把每个代币的价格记录下来，单位是 **wei** （ ETH 的最小单位），就像一块钱拆成一分、一厘一样。如果`tokenPrice = 10**16`，就代表**一个代币的成本为 0.01 ETH** 。

---

```json
saleStartTime = block.timestamp;

```

我们将发售的开始时间标记为**现在**——即合约部署的那一秒。

---

```json
saleEndTime = block.timestamp + _saleDurationInSeconds;

```

发售活动是限时的。因此，我们通过在开始时间上**添加持续时间（以秒为单位）** 来设置结束时间。

如果你传入 `604800`（一周的秒数），那就是 7 天后发售结束。

---

```json
minPurchase and maxPurchase

```

这两行定义了买家可以发送的 ETH 数量的**限制**：

- `minPurchase` 防止有人只买一点点。
- `maxPurchase` 防止有“大户”或机器人一次买太多。

这两个值也以 **wei** 为单位。

---

```json
projectOwner = _projectOwner;

```

我们把项目所有者（即应该在发售期间接收筹集的ETH 的人）地址存储下来。

发售结束后，只有这个人可以调用 `finalizeSale()` 并提取资金。

---

```json
_transfer(msg.sender, address(this), totalSupply);

```

有趣的地方来了！

- 此时，部署者（你）拥有所有代币——因为 ERC-20 的规则就是这样。
- 但你不想**手动**一笔一笔将这些代币转给发售合约。
- 所以在构造函数里，程序会**自动地将所有代币从部署者（你）转移至合约里**。

这样，发售合约就变成了“代币分发员”。

---

```json
initialTransferDone = true;

```

这句是做个标记，告诉系统“从部署者到合约的代币交接已经完成”。

这个布尔值会在 `transfer()` 函数中使用，用来确保锁定功能只有在代币已转入合约**之后**才生效。

---

## `isSaleActive()`

```solidity
function isSaleActive() public view returns (bool) {
    return (!finalized && block.timestamp >= saleStartTime && block.timestamp <= saleEndTime);
}

```

这个函数是用来检查发售是否正在进行：

- finalized 不能是 true（说明发售还没结束）
- 当前时间必须在发售时间窗口内

整个合约会用这个函数来判断什么时候可以买代币。

---

## `buyTokens()` — 主要购买函数

```solidity
function buyTokens() public payable {
    require(isSaleActive(), "Sale is not active");
    require(msg.value >= minPurchase, "Amount is below minimum purchase");
    require(msg.value <= maxPurchase, "Amount exceeds maximum purchase");

    uint256 tokenAmount = (msg.value * 10**uint256(decimals)) / tokenPrice;
    require(balanceOf[address(this)] >= tokenAmount, "Not enough tokens left for sale");

    totalRaised += msg.value;
    _transfer(address(this), msg.sender, tokenAmount);
    emit TokensPurchased(msg.sender, msg.value, tokenAmount);
}

```

这个函数就是用户在发售期间买代币时要用的。他们会调用 `buyTokens()` 并随交易发送 ETH。具体流程如下：

---

### 1. 检查发售是否还在进行

```solidity
require(isSaleActive(), "Sale is not active");

```

先用辅助函数`isSaleActive()`检查发售是否正在进行中。如果发售已结束、尚未开始或已完成，则此检查失败，交易将被撤销。

---

### 2. **执行最低和最高购买限额**

```solidity
require(msg.value >= minPurchase, "Amount is below minimum purchase");
require(msg.value <= maxPurchase, "Amount exceeds maximum purchase");

```

我们想要控制用户在一笔交易中可以发送的 ETH 的最小和最大金额。这有助于防止垃圾购买或来自机器人的巨额购买。这两行确保发送的 ETH 金额在允许范围内。

我们希望控制用户在单笔交易中可以发送 ETH 的最小和最大金额。防止有人恶意刷单或者大户一次买完。这两行代码确保发送的 ETH 数量在允许的范围内。

---

### 3. 计算要发多少代币

```solidity
uint256 tokenAmount = (msg.value * 10**uint256(decimals)) / tokenPrice;

```

现在我们计算买家发送的 ETH 应该获得多少代币。

- 我们将 ETH 数量(`msg.value`) 乘以`10 ** decimals` （请记住：大多数 ERC-20 代币有 18 位小数）。
- 再除以每个代币的价格 `tokenPrice`。

这能算出买家能收到的代币数量。

---

### 4. 确保合约里有足够的代币

```solidity
require(balanceOf[address(this)] >= tokenAmount, "Not enough tokens left for sale");

```

即使用户发送了有效的 ETH 并申请了合理数量的代币，我们还要检查合约里是否持有足够的代币来满足请求。如果没有，购买将被阻止。

---

### 5. 更新已筹集的 ETH总额

```solidity
totalRaised += msg.value;

```

这句代码会记录本次发售中接收的 ETH 的累计总额。发售结束时，我们会用这个总额来结账。

---

### 6. 把代币转给买家

```solidity
_transfer(address(this), msg.sender, tokenAmount);

```

这里就是代币真的“动起来”的地方。合约会把自己账户里的代币转给买家。

- `address(this)` 是合约的地址（发售期间所有代币都放在这里）。
- `msg.sender` 是买家的地址。

这个 `_transfer()` 函数是在我们之前构建的 ERC-20 代币中定义的。

---

### 7. 触发购买事件

```solidity
emit TokensPurchased(msg.sender, msg.value, tokenAmount);

```

我们触发 `TokensPurchased` 事件，记录：

- 谁买代币
- 花了多少 ETH
- 收到多少代币

---

## 重写 `transfer()` — 锁定直接转账

```solidity
function transfer(address _to, uint256 _value) public override returns (bool) {
    if (!finalized && msg.sender != address(this) && initialTransferDone) {
        require(false, "Tokens are locked until sale is finalized");
    }
    return super.transfer(_to, _value);
}

```

这个函数重写了标准的 ERC-20 `transfer()` 函数。这里的主要目的是在发售进行期间**暂时限制代币转账**。

我们逐步分析：

### 1. 检查发售

```solidity
if (!finalized && msg.sender != address(this) && initialTransferDone) {
    require(false, "Tokens are locked until sale is finalized");
}

```

此条件检查三件事：

- `!finalized`：发售**尚未**完成。
- `msg.sender != address(this)`：交易**不是由合约本身**发起的（例如在 `buyTokens()` 期间）。
- `initialTransferDone`：初始代币供应已经转移到合约中。

如果这三个条件都满足，函数会回滚，交易会被撤销 ****。

为什么要这样？

这样可以保证**发售期间**没人能提前交易代币，这有助于防止：

- 过早交易或投机
- 机器人抢购代币并抛售
- 代币上线前的操纵

一旦发售完成，这个条件变为无效，大家可以自由转账。

### 2. 正常转账

```solidity
return super.transfer(_to, _value);

```

如果发售已完成（或者发送者是合约本身），我们只需调用父合约的原始 `transfer()` 函数。这会执行实际的转账逻辑。

---

## 重写 `transferFrom()` — 锁定委托转账

```solidity
function transferFrom(address _from, address _to, uint256 _value) public override returns (bool) {
    if (!finalized && _from != address(this)) {
        require(false, "Tokens are locked until sale is finalized");
    }
    return super.transferFrom(_from, _to, _value);
}

```

就像 `transfer()` 一样，这个函数重写

了 ERC-20 的 `transferFrom()`——但它用于**委托转账**，即某人代表另一个钱包消费代币（通常在 `approve()` 调用之后）。

让我们分解一下：

### 1. 发售锁定检查

```solidity
if (!finalized && _from != address(this)) {
    require(false, "Tokens are locked until sale is finalized");
}
```

我们再次检查：

- 发售还在继续吗？
- 转账是否不是来自合约？

如果是，交易停止。这确保了**即使是已获批准的消费者，**也不能在发售期间以他人的名义转移代币。

为什么？

因为有人可能会批准一份合约（比如 Uniswap），并且该合约甚至可以在发售进行时交易代币——这就违背了转移锁定的整个目的。

### 2. 恢复默认逻辑

```solidity
return super.transferFrom(_from, _to, _value);

```

如果检查通过（发售完成或转移由合约发起），我们使用 `super` 回退到原始的 ERC-20 `transferFrom()` ，恢复默认的转账逻辑。

---

## `finalizeSale()`

```solidity
function finalizeSale() public payable {
    require(msg.sender == projectOwner, "Only Owner can call the function");
    require(!finalized, "Sale already finalized");
    require(block.timestamp > saleEndTime, "Sale not finished yet");

    finalized = true;
    uint256 tokensSold = totalSupply - balanceOf[address(this)];

    (bool success, ) = projectOwner.call{value: address(this).balance}("");
    require(success, "Transfer to project owner failed");

    emit SaleFinalized(totalRaised, tokensSold);
}

```

这是**结束代币发售**的函数——就像在发布日结束后关店一样。让我们逐步分析：

---

### 访问控制和**计时**

```solidity
require(msg.sender == projectOwner, "Only Owner can call the function");
require(!finalized, "Sale already finalized");
require(block.timestamp > saleEndTime, "Sale not finished yet");

```

这三个 `require()` 检查执行以下操作：

1. **只有项目所有者**可以调用此函数——防止无关用户干扰发售流程。
2. 检查是否已完成发售——如果已完成，则不允许再次调用。
3. 确保发售期已结束（依据结束时间戳）——从而不能提前终止发售。

---

### 将发售标记为完成

```solidity
finalized = true;

```

我们把 `finalized` 状态变量设置为已完成，以便其他函数（如 `transfer()` 和 `transferFrom()`）识别发售已结束。随后系统将解除锁定，恢复自由转账。

---

### 计算已售出的代币数量

```solidity
uint256 tokensSold = totalSupply - balanceOf[address(this)];

```

如下计算本次实际售出的代币：

- `totalSupply` 表示代币的总发行量。
- `balanceOf[address(this)]` 表示合约当前持有的代币余额（即尚未售出部分）。
- 两者相减即为已分配给用户的代币数量。

---

### 向项目所有者发送 ETH

```solidity
(bool success, ) = projectOwner.call{value: address(this).balance}("");
require(success, "Transfer to project owner failed");

```

此处把发售期间筹集到的**全部 ETH** 转给  `projectOwner`。

- `address(this).balance` 表示该合约地址当前持有的 ETH 总额。
- 我们使用 `.call{value: ...}` 来发送资金。
- `require(success)` 用于确保这次发送没有静默失败（即失败但无报错提示）。

项目方可以此收取本次发售筹得的 ETH

---

### 触发最终事件

```solidity
emit SaleFinalized(totalRaised, tokensSold);

```

最后，我们触发 `SaleFinalized` 事件，其中包含：

- 筹集的 ETH总额
- 售出的代币数量

前端页面、DApp 或区块浏览器可监听该事件，从而向用户显示发售已正式完成。

---

## `timeRemaining()` 和 `tokensAvailable()` — 发售状态辅助函数

这两个只读函数为前端、看板或其他智能合约提供便捷的实时信息查询能力。

---

### `timeRemaining()`

```solidity
function timeRemaining() public view returns (uint256) {
    if (block.timestamp >= saleEndTime) {
        return 0;
    }
    return saleEndTime - block.timestamp;
}

```

该函数返回距离发售结束还剩余多少秒。

- 它将**当前区块时间戳** (`block.timestamp`) 与预定义的 `saleEndTime` 进行比较。
- 如果发售已经结束，返回 `0`。
- 否则，返回当前时间和结束时间之间的差值。

**使用场景：** 前端可以调用此函数来显示倒计时，例如"还剩 2 小时 15 分钟"——既能体现紧迫感，也方便用户把握最后参与时机。

---

### `tokensAvailable()` 可购买代币数量

```solidity
function tokensAvailable() public view returns (uint256) {
    return balanceOf[address(this)];
}

```

该函数返回当前**可购买**的代币数量。

- 发售期内，发售合约持有全部待售代币。
- 所以我们只需使用 `balanceOf[address(this)]` 返回其当前余额。

**使用场景：** 前端或 DApp 可显示"剩余 X 个代币"，让买家及时了解库存、提升决策效率。

---

### 这些辅助函数为何有用

尽管实现简单，它们对提升发售的**易用性**作用显著：

- 它们可提供实时数据展示
- 作为 view 函数，对外部调用不消耗 gas（链下读取）
- 同时让合约逻辑更清晰、易读、易集成

无论是开发精美的可视化看板，还是调试合约，这些辅助函数都会显著降低工作难度。

---

## `receive()` — ETH回退处理器

```solidity
receive() external payable {
    buyTokens();
}

```

该函数显著提升购买流程的丝滑与直觉性——对非技术用户尤为友好。

在 Solidity 中，`receive()` 函数是一个**特殊的回退函数**，在满足以下条件时被触发：

- 有人**直接**向合约地址发送 ETH
- 且**未指定**要调用的任何函数

通常若合约未定义该函数，外部直接转入 ETH 的交易会失败。

但在这种情况下，我们定义了 `receive()`，只要有人向该合约转入 ETH（即使只是从 MetaMask 或简单的钱包转账），合约都会在后台**自动调用`buyTokens()`完成购买流程**。

这意味着：

- 用户无需进入 dApp 的界面操作
- 无需手动调用 `buyTokens()` 函数
- 只需发送 ETH ，即可参与发售

虽是小改动，却显著改善用户体验——即使用户忘记调用购买函数，也仍能获得代币。

总之：`receive()` 既是安全兜底，又是快速通道。

它的功能是允许ETH流入，并将 ETH 直接路由到代币销售逻辑中。

---

## 🎯 总结

至此，你已从零搭建出一个完整的 **ERC-20 代币发售**系统。

你做的不仅是写一个智能合约——而是完成了一个真实加密项目常用的**预售系统**。你已经掌握了：

- 如何部署自定义 ERC-20 代币
- 如何搭建含定价逻辑的限时发售
- 如何用 ETH 完成购买并实时分发代币
- 如何在发售期锁定转账以抑制投机行为
- 如何结束发售并把筹得资金安全转入项目钱包

更重要的是——您了解了每个部分的**重要性**。

从保护买家、维护公平，到精确的 ETH ↔代币换算、到更加用户友好的 `receive()` 函数——您已经看到智能合约如何把这些要素整合在一起。

这套架构是诸多 DeFi 发行、DAO 募资，甚至 NFT 空投（略有调整）的支柱。您已经踏入了由**智能合约驱动的众筹**世界。