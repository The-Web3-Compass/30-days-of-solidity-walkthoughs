# 去中心化管理DAO

Day: Day 28
ID: 28
原文: https://builder-hub.notion.site/DecentralisedGovernance-Contract-1e25720a23ef8014a8caf30dca28fb9b
状态: 完成
译者: 雷雷
难度等级: 高级

# **学习内容**

欢迎回到 **30 Days of Solidity**。

不久前，你写了你的第一份**投票合同voting contract** ——

一种简单、优雅的方式，让用户提出想法并让其他人投“是”或“否”票。

这是您进入链上决策世界的第一步。

但今天呢？

今天，我们将其提升到一个全新的水平。

因为真正的去中心化系统不仅仅停留在收集选票上。

他们需要真正的**治理 governance**——

具有规则、保护、问责制和执行功能——所有这些都直接内置于代码中。

这正是我们接下来要构建的。

在我们深入研究合约本身之前，让我们花点**时间了解为什么**治理很重要——

以及我们所说的 **DAO** 到底是什么意思。

### ****

### **什么是 DAO？**

[🌐](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

**DAO**—— **去中心化自治组织Decentralized Autonomous Organization**的缩写——

是一种由**社区而不是公司**来管理节目的新型系统。没有首席执行官。没有经理。没有律师闭门起草合同。相反，一切都在**链上**透明地发生，基于社区的投票。

想象一下一个在线俱乐部，其中：

每个持有代币的人都可以对发生的事情发表意见。

重大决策——例如如何使用资金、构建哪些功能或发生哪些升级——都是由投票决定的。

一旦投票通过，智能合约**就会自动**执行决定，无需等待人工批准。

这是**纯粹的民主** ，用 Solidity 编写。

如果你听说过像 **Uniswap**、**Aave** 或 **Compound** 这样的项目——它们都由他们的社区管理，使用与**您将要构建的 DAO 系统一样**运行的 DAO 系统。

### **😎这与我们之前的投票合同有何不同？**

[✨](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

当你之前构建简单的投票系统时，你学会了如何收集选票和决定结果。

今天的项目增加了现实世界的复杂性——但以一种仍然对初学者友好的方式。

在这个升级后的 DAO 系统中：

- 用户在提出想法时会**锁定一笔小额押金small deposit** ，以防止垃圾邮件。
- **投票权Voting power**将取决于某人拥有**多少代币tokens** ——这意味着更大的利益相关者拥有更大的发言权。
- 提案必须**达到法定人数meet a quorum** （最低参与水平）才能有效。
- 即使提案获胜，也无法立即执行。
- **时间锁timelock**确保在任何事情发生之前有一个公开的等待期，让每个人都有时间在需要时做出反应。

而到时候，提案**才会真正执行实际行动** ——就像自动调用其他合约上的函数一样。

这不仅仅是“让我们投票”。这是**让我们投票、验证、延迟和执行——完整的现实世界流程** 。

### **为什么这是如此巨大的一步**

[🚀](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

今天，你不仅仅是在学习 Solidity 语法。你正在学习**区块链社会如何自我管理。**

你正在学习系统如何：

国库分配Treasury allocations

协议升级Protocol upgrades

功能更改Feature changes

生态系统资金Ecosystem funding

**不是由公司**决定的，而**是由社区决定**的，由不可阻挡的智能合约强制执行。

你正在学习**权力是如何分配**的，以及**信任是如何从人转移到代码的。**

你正在学习如何构建一个不需要**许可**的系统——规则就在那里，清晰而自动，每个人都可以看到。

### **您将要构建的内容**

[🎯](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

在此项目中，你将创建一个智能合约，让任何人：

提出一个想法（锁定代币存款后）

让社区根据他们持有的代币数量进行投票

确保参与的选民人数最少（法定人数）

在投票后添加等待期（时间锁）

如果投票通过，则自动执行获胜提案的作

所有这一切—— **公平** 、 **透明**和**无需信任** 。

这是真正的 DeFi 平台每天使用的系统——现在，你要亲手写出来。

[🚀](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

[🌾](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

总之，本去中心化治理合约构建了一个基于区块链的透明决策系统，通过智能合约实现提案创建、加权投票和时间锁执行的全流程自动化。该体系将治理规则编码于链上，确保投票过程公开可验证、结果不可篡改，并借助代币经济模型使投票权与持币利益相绑定。它不仅适用于DAO社区、DeFi协议升级等场景，更以代码之力保障了集体决策的公正性与执行效率，重新定义了数字时代的治理范式。

**学习目标**：掌握DAO治理机制和投票系统

**核心技能**：提案创建、加权投票、法定人数、时间锁

**完整代码**：[DecentralisedGovernance Contract](https://www.notion.so/1e25720a23ef8014a8caf30dca28fb9b?pvs=21)

# **1、产品需求书**

### **a.用户路径图**

**👨‍💼 管理员路径 (Admin)**

1. **合约部署**
    - 设置治理代币地址
    - 配置投票参数（时长、时间锁、法定人数比例）
    - 设置提案押金金额
    - 自动成为初始管理员
2. **系统管理**
    - 调整法定人数百分比
    - 更新提案押金金额
    - 监控系统运行状态

**📝 提案者路径 (Proposer)**

1. **提案准备**
    - 确保持有足够治理代币用于押金
    - 准备详细的提案描述
2. **创建提案**
    - 调用`createProposal()`函数
    - 支付提案押金（如果设置）
    - 提案进入投票期
3. **结果跟踪**
    - 监控投票进展
    - 等待提案最终化
    - 成功后退还押金

**🗳️ 投票者路径 (Voter)**

1. **资格验证**
    - 持有治理代币（投票权重基础）
    - 确认在投票期内
2. **投票执行**
    - 选择支持或反对
    - 基于代币余额计算投票权重
    - 确认投票交易
3. **结果确认**
    - 查看投票是否被记录
    - 跟踪提案最终结果
    

    

### **b.数据库结构**

**合约基本信息**

| **Contract / 合约** | **Type / 类型** | **Bases / 基础合约** | **含义 / Description** |
| --- | --- | --- | --- |
| DecentralizedGovernance | Implementation / 实现合约 | ReentrancyGuard | 去中心化治理合约 |

**状态变量表**

| **L** | **Field Name / 字段名** | **Visibility / 可见性** | **Mutability / 可变性** | **Data Type / 数据类型** | **Description / 描述** |
| --- | --- | --- | --- | --- | --- |
| L | governanceToken | Public / 公开 | view | IERC20 | 治理代币合约接口 |
| L | nextProposalId | Public / 公开 | view | uint256 | 下一个提案ID |
| L | votingDuration | Public / 公开 | view | uint256 | 投票持续时间（秒） |
| L | timelockDuration | Public / 公开 | view | uint256 | 时间锁持续时间（秒） |
| L | admin | Public / 公开 | view | address | 管理员地址 |
| L | quorumPercentage | Public / 公开 | view | uint256 | 法定人数百分比 |
| L | proposalDepositAmount | Public / 公开 | view | uint256 | 提案押金金额 |
| L | proposals | Public / 公开 | view | mapping(uint256 ⇒ Proposal) | 提案ID到提案结构的映射 |
| L | hasVoted | Public / 公开 | view | mapping(uint256 ⇒ mapping(address ⇒ bool)) | 投票记录映射 |

### **结构体定义**

| **L** | **Struct Name / 结构体名** | **Field / 字段** | **Data Type / 数据类型** | **Description / 描述** |
| --- | --- | --- | --- | --- |
| L | Proposal | proposer | address | 提案创建者地址 |
| L | Proposal | description | string | 提案描述内容 |
| L | Proposal | forVotes | uint256 | 支持票数（加权） |
| L | Proposal | againstVotes | uint256 | 反对票数（加权） |
| L | Proposal | startTime | uint256 | 投票开始时间 |
| L | Proposal | endTime | uint256 | 投票结束时间 |
| L | Proposal | executed | bool | 是否已执行 |
| L | Proposal | canceled | bool | 是否已取消 |
| L | Proposal | timelockEnd | uint256 | 时间锁结束时间 |

### **函数接口表**

| **L** | **Function Name / 函数名** | **Visibility / 可见性** | **Mutability / 可变性** | **Modifiers / 修饰器** | **Description / 描述** |
| --- | --- | --- | --- | --- | --- |
| L | constructor | Internal / 内部 | state-changing | NO | 合约构造函数 |
| L | createProposal | Public / 公开 | state-changing | NO | 创建新提案 |
| L | vote | Public / 公开 | state-changing | NO | 对提案投票 |
| L | finalizeProposal | Public / 公开 | state-changing | NO | 最终确定提案结果 |
| L | executeProposal | Public / 公开 | state-changing | NO | 执行通过时间锁的提案 |
| L | getProposalResult | Public / 公开 | view | NO | 获取提案结果 |
| L | setQuorumPercentage | Public / 公开 | state-changing | onlyAdmin | 设置法定人数百分比 |
| L | setProposalDepositAmount | Public / 公开 | state-changing | onlyAdmin | 设置提案押金金额 |
| L | _refundDeposit | Internal / 内部 | state-changing | NO | 内部退还押金函数 |

**事件日志表**

| **L** | **Event Name / 事件名** | **Parameters / 参数** | **Description / 描述** |
| --- | --- | --- | --- |
| L | ProposalCreated | (uint256 proposalId, address proposer, string description) | 提案创建事件 |
| L | Voted | (uint256 proposalId, address voter, bool support, uint256 weight) | 投票事件 |
| L | ProposalExecuted | (uint256 proposalId) | 提案执行事件 |
| L | QuorumNotMet | (uint256 proposalId) | 法定人数不足事件 |
| L | ProposalTimelockStarted | (uint256 proposalId) | 时间锁启动事件 |

**修饰器表**

| **L** | **Modifier Name / 修饰器名** | **Description / 描述** |
| --- | --- | --- |
| L | onlyAdmin | 仅管理员可调用 |
| L | nonReentrant | 防重入攻击保护（继承自ReentrancyGuard） |

### **🔄 治理流程关键节点**

**⏰ 时间节点控制**

1. **投票期**：`startTime` → `endTime`（固定时长）
2. **时间锁期**：投票通过后 → `timelockEnd`（额外等待期）
3. **执行窗口**：时间锁结束后无限制

**📊 投票权重计算**

- **基础**：治理代币余额
- **公式**：`weight = governanceToken.balanceOf(voter)`
- **特性**：1代币 = 1投票权

**✅ 通过条件**

1. **法定人数**：`总票数 ≥ 总供应量 × 法定人数百分比`
2. **多数支持**：`支持票 > 反对票`
3. **时间要求**：投票期结束且时间锁期满

# 2、细节拆解

### **核心概念**

**🏛️ （1）DAO治理原理**

去中心化自治组织（DAO）通过智能合约实现民主治理：

**✅ DAO核心特征**

- 代码即法律，规则透明
- 代币持有者参与治理
- 提案投票决定重大事项
- 自动执行通过的提案
- 无需中心化管理

**治理流程：**

`创建提案 → 投票期 → 计票 → 时间锁 → 执行`

**🗳️ （2）加权投票机制**

基于代币持有量的加权投票确保利益相关者的话语权：

```solidity
function vote(uint256 proposalId, bool support) external {
    Proposal storage proposal = proposals[proposalId];
    require(!hasVoted[proposalId][msg.sender], "Already voted");

    // 投票权重 = 代币余额
    uint256 weight = governanceToken.balanceOf(msg.sender);
    require(weight > 0, "No voting power");

    hasVoted[proposalId][msg.sender] = true;

    if (support) {
        proposal.forVotes += weight;
    } else {
        proposal.againstVotes += weight;
    }

    emit Voted(proposalId, msg.sender, support, weight);
}
```

**投票权重计算：**

- **持币投票：**代币数量 = 投票权重
- **防重复投票：**hasVoted映射记录
- **实时权重：**基于当前余额
- **透明记录：**所有投票公开可查

📊 **（3）法定人数机制**

法定人数确保提案有足够的参与度才能通过：

```solidity
function finalizeProposal(uint256 proposalId) external {
    Proposal storage proposal = proposals[proposalId];
    require(block.timestamp > proposal.endTime, "Voting not ended");

    uint256 totalVotes = proposal.forVotes + proposal.againstVotes;
    uint256 totalSupply = governanceToken.totalSupply();
    uint256 quorumRequired = (totalSupply * quorumPercentage) / 100;

    if (totalVotes >= quorumRequired && proposal.forVotes > proposal.againstVotes) {
        // 提案通过，进入时间锁
        proposal.timelockEnd = block.timestamp + timelockDuration;
        emit ProposalTimelockStarted(proposalId);
    } else {
        proposal.canceled = true;
        emit QuorumNotMet(proposalId);
    }
}
```

**通过条件：**

**双重检查机制**

1. 总投票数 ≥ 法定人数
2. 赞成票 > 反对票

**⏰ （4）时间锁保护**

时间锁为社区提供反应时间，防止恶意提案：

```solidity
function executeProposal(uint256 proposalId) external {
    Proposal storage proposal = proposals[proposalId];

    require(proposal.timelockEnd > 0, "No timelock set");
    require(block.timestamp >= proposal.timelockEnd, "Timelock not ended");
    require(!proposal.executed, "Already executed");

    proposal.executed = true;

    // 退还提案押金
    if (proposalDepositAmount > 0) {
        governanceToken.transfer(proposal.proposer, proposalDepositAmount);
    }

    emit ProposalExecuted(proposalId);
}
```

**时间锁作用：**

- **缓冲期：**给社区时间审查提案
- **退出机会：**不同意者可以退出
- **安全保障：**防止闪电攻击
- **透明执行：**执行时间可预期

## 完整代码详解

### 📄 DAO治理合约

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract DecentralizedGovernance is ReentrancyGuard {
    IERC20 public governanceToken;

    struct Proposal {
        address proposer;
        string description;
        uint256 forVotes;
        uint256 againstVotes;
        uint256 startTime;
        uint256 endTime;
        bool executed;
        bool canceled;
        uint256 timelockEnd;
    }

    mapping(uint256 => Proposal) public proposals;
    mapping(uint256 => mapping(address => bool)) public hasVoted;

    uint256 public nextProposalId;
    uint256 public votingDuration;
    uint256 public timelockDuration;
    address public admin;
    uint256 public quorumPercentage;
    uint256 public proposalDepositAmount;

    event ProposalCreated(uint256 indexed proposalId, address indexed proposer, string description);
    event Voted(uint256 indexed proposalId, address indexed voter, bool support, uint256 weight);
    event ProposalExecuted(uint256 indexed proposalId);
    event QuorumNotMet(uint256 indexed proposalId);
    event ProposalTimelockStarted(uint256 indexed proposalId);

    modifier onlyAdmin() {
        require(msg.sender == admin, "Not admin");
        _;
    }

    constructor(
        address _governanceToken,
        uint256 _votingDuration,
        uint256 _timelockDuration,
        uint256 _quorumPercentage,
        uint256 _proposalDepositAmount
    ) {
        require(_governanceToken != address(0), "Invalid token");
        require(_votingDuration > 0, "Invalid duration");
        require(_quorumPercentage > 0 && _quorumPercentage <= 100, "Invalid quorum");

        governanceToken = IERC20(_governanceToken);
        votingDuration = _votingDuration;
        timelockDuration = _timelockDuration;
        admin = msg.sender;
        quorumPercentage = _quorumPercentage;
        proposalDepositAmount = _proposalDepositAmount;
    }

    // 创建提案
    function createProposal(string memory description) external returns (uint256) {
        require(bytes(description).length > 0, "Empty description");

        // 收取提案押金
        if (proposalDepositAmount > 0) {
            governanceToken.transferFrom(msg.sender, address(this), proposalDepositAmount);
        }

        uint256 proposalId = nextProposalId++;

        proposals[proposalId] = Proposal({
            proposer: msg.sender,
            description: description,
            forVotes: 0,
            againstVotes: 0,
            startTime: block.timestamp,
            endTime: block.timestamp + votingDuration,
            executed: false,
            canceled: false,
            timelockEnd: 0
        });

        emit ProposalCreated(proposalId, msg.sender, description);
        return proposalId;
    }

    // 投票
    function vote(uint256 proposalId, bool support) external {
        Proposal storage proposal = proposals[proposalId];

        require(block.timestamp >= proposal.startTime, "Not started");
        require(block.timestamp <= proposal.endTime, "Ended");
        require(!proposal.executed, "Already executed");
        require(!proposal.canceled, "Canceled");
        require(!hasVoted[proposalId][msg.sender], "Already voted");

        uint256 weight = governanceToken.balanceOf(msg.sender);
        require(weight > 0, "No voting power");

        hasVoted[proposalId][msg.sender] = true;

        if (support) {
            proposal.forVotes += weight;
        } else {
            proposal.againstVotes += weight;
        }

        emit Voted(proposalId, msg.sender, support, weight);
    }

    // 完成提案（进入时间锁或取消）
    function finalizeProposal(uint256 proposalId) external {
        Proposal storage proposal = proposals[proposalId];

        require(block.timestamp > proposal.endTime, "Voting not ended");
        require(!proposal.executed, "Already executed");
        require(!proposal.canceled, "Canceled");

        uint256 totalVotes = proposal.forVotes + proposal.againstVotes;
        uint256 totalSupply = governanceToken.totalSupply();
        uint256 quorumRequired = (totalSupply * quorumPercentage) / 100;

        if (totalVotes >= quorumRequired && proposal.forVotes > proposal.againstVotes) {
            if (timelockDuration > 0) {
                proposal.timelockEnd = block.timestamp + timelockDuration;
                emit ProposalTimelockStarted(proposalId);
            } else {
                proposal.executed = true;
                _refundDeposit(proposalId);
                emit ProposalExecuted(proposalId);
            }
        } else {
            proposal.canceled = true;
            emit QuorumNotMet(proposalId);
        }
    }

    // 执行提案
    function executeProposal(uint256 proposalId) external {
        Proposal storage proposal = proposals[proposalId];

        require(proposal.timelockEnd > 0, "No timelock set");
        require(block.timestamp >= proposal.timelockEnd, "Timelock not ended");
        require(!proposal.executed, "Already executed");
        require(!proposal.canceled, "Canceled");

        proposal.executed = true;
        _refundDeposit(proposalId);

        emit ProposalExecuted(proposalId);
    }

    // 退还押金
    function _refundDeposit(uint256 proposalId) internal {
        if (proposalDepositAmount > 0) {
            Proposal storage proposal = proposals[proposalId];
            governanceToken.transfer(proposal.proposer, proposalDepositAmount);
        }
    }

    // 获取提案结果
    function getProposalResult(uint256 proposalId) external view returns (
        bool passed,
        uint256 forVotes,
        uint256 againstVotes,
        bool executed
    ) {
        Proposal memory proposal = proposals[proposalId];
        passed = proposal.forVotes > proposal.againstVotes;
        return (passed, proposal.forVotes, proposal.againstVotes, proposal.executed);
    }

    // 管理员设置参数
    function setQuorumPercentage(uint256 _newQuorum) external onlyAdmin {
        require(_newQuorum > 0 && _newQuorum <= 100, "Invalid quorum");
        quorumPercentage = _newQuorum;
    }

    function setProposalDepositAmount(uint256 _newAmount) external onlyAdmin {
        proposalDepositAmount = _newAmount;
    }
}
```

**✅ DAO治理流程**

任何人创建提案 → 代币持有者投票 → 达到法定人数且多数赞成 → 进入时间锁 → 社区审查期 → 自动执行。整个过程透明、民主、不可篡改。

## **（1）导入 — 引入我们需要的工具Imports — Bringing in the Tools**

在我们开始构建实际的治理逻辑之前，

我们从 OpenZeppelin 中提取了一些重要的工具——

用于安全、经过审计的智能合约代码的可信库。

您可以将这些进口物品想象成在开始建造严肃的东西之前拿起你的**锤子、扳手和头盔** 。

以下是我们要导入的内容：

```jsx
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/utils/math/SafeCast.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
```

### **每个导入的作用**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **`IERC20.sol`**

[🪙](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这带来了 ERC-20 代币的**接口** ——

这意味着它定义了任何 ERC-20 代币必须具备的基本功能（balanceOf、transfer、transferFrom 等）。

我们使用它，以便我们的治理合约可以与任何用于投票权的 ERC-20 代币**进行对话** ——

而不关心其完整的内部实现。

 无论您的代币是简单、复杂还是花哨——

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

只要遵循 ERC-20，我们的合约就可以与之交互。

### **`SafeCast.sol`**

[🔢](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此实用程序有助于**进行安全的类型转换** 。

在 Solidity 中，有时您需要在 uint256 和较小的类型（如 uint32、uint64 等）之间进行转换。

如果没有安全检查，错误的转换可能会导致**溢出**或**静默的错误** 。

 SafeCast 确保您缩小的任何号码都**不会丢失重要数据** ——否则，交易将自动恢复。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

在我们的例子中，它主要作为最佳实践在这里——因为我们处理的是时间、投票和其他需要安全的数值操作。

### **`ReentrancyGuard.sol`**

[🛡️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这增加了对称为**重入攻击**的讨厌类别攻击的保护。

快速复习：

当智能合约在更新自己的内部状态之前调用外部地址时，就会发生重入攻击——

并且该外部地址恶意**回调**合约并**滥用**未完成的逻辑。

 通过使用 `ReentrancyGuard`，我们可以**锁定**像 `executeProposal（）` 这样的函数，这样在第一次调用完成之前**就无法再次输入**它们。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这在移动代币或执行外部调用时绝对至关重要。

## （2）**合约声明 — 奠定基础Contract Declaration — Laying the Foundation**

现在我们已经导入了所有基本工具，是时候为我们的治理体系奠定**基础**了。

以下是我们正式开始构建合同的方式：

```jsx
contract DecentralizedGovernance is ReentrancyGuard {
    using SafeCast for uint256;
```

### **这是怎么回事？**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **`contract DecentralizedGovernance is ReentrancyGuard`**

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这一行**声明**了我们的智能合约并为其命名：

 `DecentralizedGovernance` 的。

[👉](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

它还**继承自**  `ReentrancyGuard`，

这意味着我们的合约会自动获得关键功能的重入保护——尤其是当我们执行可能调用其他合约的提案时。

 这可以保护我们免受攻击者在执行过程中尝试双花或递归调用等偷偷摸摸的伎俩。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **`using SafeCast for uint256`**

[🧹](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这句话说：

“嘿，Solidity——只要我有 uint256，让我自动访问 **SafeCast** 功能。

这就像赋予 `uint256` 变量额外的**超能力** ——

允许他们在需要时安全地将自己降级到更小的类型（`uint32、uint64` 等），

而不必每次都手动调用 `SafeCast.safeCastFunction（value）`。

 更干净的代码。更安全的数学运算。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## **（3） 设置核心数据：提案和投票记录Setting Up the Core Data: Proposals and Voting Records**

[🧩](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在合同已经声明，

下一步是定义我们的 DAO 将管理**什么样的信息** 。任何治理系统的核心都有一个关键： **提案** 。

我们需要一种存储方式：

该提案的内容、谁创造了它、人们如何投票、无论通过还是失败

如果成功，应该采取什么行动

现在让我们为所有这些设置结构。

### **结构体 + 映射  的完整代码Full Code for Struct + Mappings**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
struct Proposal {
    uint256 id;
    string description;
    uint256 deadline;
    uint256 votesFor;
    uint256 votesAgainst;
    bool executed;
    address proposer;
    bytes[] executionData;
    address[] executionTargets;
    uint256 executionTime;
}

mapping(uint256 => Proposal) public proposals;
mapping(uint256 => mapping(address => bool)) public hasVoted;
```

### **深入探讨：逐行细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **`struct Proposal`**

[🏛️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此**蓝图**定义了**单个提案**在合同中的外观。

每个提案都有：

**`id`**：用于跟踪的唯一 ID 号。

**`description`**：对提案内容的人类可读解释。

**`deadline`**：投票结束时的时间戳标记。

**`votesFor`**：有多少票投了赞成票。

**`votesAgainst`**：有多少票投了反对票。

**`executed`**：一个布尔值（true/false），用于跟踪提案是否已执行。

**`proposer`**：创建提案的人的地址。

**`executionData`**：如果提案通过，将在其他合约上调用的实际数据有效负载。

**`executionTargets`**：这些有效负载应发送到的合约地址列表。

**`executionTime`**：时间锁后提案可以正式执行的未来时间戳。

 这个结构包含了我们从头到尾完全管理 DAO 提案所需的**一切** ——

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

从创建→投票→执行。

```jsx
mapping(uint256 => Proposal) public proposals;
```

这将**设置映射或查找表** ，

其中每个 Proposal 结构都通过其唯一 ID 存储和访问。

可以这样想：

“提案 ID #2 →整个提案结构体”

 这允许合约（和用户）仅通过其 ID 号快速获取有关任何提案的详细信息。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 标记为**公共** ，因此外部用户和前端可以轻松获取提案数据，而无需编写额外的 getter 函数。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
mapping(uint256 => mapping(address => bool)) public hasVoted;
```

这是一个**双重映射** ——有点棘手，但非常重要。

第一个键是 `proposalId`

第二个关键是选民的地址`voter's address`

该值是一个简单的布尔`bool` 值（true/false）

这使我们**能够跟踪特定用户是否已经**对特定提案进行了投票。

 它可以防止重复投票。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 它保持投票过程的公平和干净。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 **设置治理规则**

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在我们已经定义了提案的存储**和跟踪方式** ，

下一步是建立控制**治理实际运作方式**的**规则** 。

每个 DAO 都需要回答以下重要问题：

谁可以投票？

投票开放多长时间？

谁可以创建提案？

提案需要多少人参与才能有效？

下一组变量定义了所有这些。

### ****

## **（4）治理设置的完整代码Setting Up the Governance Rules**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
IERC20 public governanceToken;
uint256 public nextProposalId;
uint256 public votingDuration;
uint256 public timelockDuration;
address public admin;
uint256 public quorumPercentage = 5;
uint256 public proposalDepositAmount = 10;
```

### **深入探讨：逐行细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
IERC20 public governanceToken;
```

这是整个系统中**最重要的部分之一** 。

**governanceToken** 是代表**投票权**的 **ERC-20 代币** 。

持有此代币的人有权对提案进行**投票** 。

你拥有**的代币越多** ，您的投票权重就越**强** 。

如果你不持有任何治理代币，则无法投票。

 该代币成为你 DAO 的“公民徽章”。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 它赋予用户提出想法、对更改进行投票并拥有真正发言权的权利。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 重要：这个代币可以是任何东西——它可以是自定义 DAO 代币、包装质押代币，甚至是真实 DAO 中的 UNI（Uniswap）或 COMP（Compound）之类的东西。

[🔥](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

我们的合约通过标准 ERC-20 接口 （`IERC20`） 与它**通信** ，

这意味着它不关心代币的内部逻辑——

只要它的行为像标准 ERC-20。

```jsx
uint256 public nextProposalId;
```

这会跟踪当有人创建新提案时将分配的**下一个 ID**。

默认情况下，它从 `0` 开始，每次创建新提案时递**增 1**。

 这可确保每个提案都获得**唯一的 ID**，而不会发生冲突。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 public votingDuration;
```

这定义了每个提案开放投票**的时间** 。

它以**秒**为单位（因为这就是时间戳在 Solidity 中的工作方式）。

一旦达到提案的截止日期（block.timestamp + votingDuration），就不允许再进行投票。

 保持投票期的**有限性和可预测**性。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 public timelockDuration;
```

这是提案获胜**后**实际执行之前的**等待期** 。

时间锁让用户有时间：

查看结果

发现任何恶意提案

如果需要，退出系统（如果发生重大变化）

 它在真正的链上作发生之前增加了**一个关键的安全缓冲区** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
address public admin;
```

这存储**了管理员地址** ——部署合约的钱包。

管理员具有一些**特殊权限** ，例如：

更改仲裁百分比

调整提案存款金额

调整时间锁持续时间

 管理员控制是有限的，主要集中在**设置参数**上，而不是干扰投票本身。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 在现实世界的 DAO 中，随着时间的推移，管理控制权通常会减少甚至被删除（逐渐去中心化治理）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 public quorumPercentage = 5;
```

这设置了提案必须参与**的总票数的最小百分比**才能有效。

例如：

如果 `quorumPercentage = 5`

代币的总供应量为 1,000,000 枚

然后至少有 5%（价值 50,000 个代币的选票）必须参与

 如果没有法定人数，一小部分选民可能会不公平地通过决定。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 法定人数确保在重大变化发生之前有**足够多的社区参与进来** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 public proposalDepositAmount = 10;
```

这定义了用户在创建提案时必须锁定**多少个治理令牌governance tokens** 。

它充当**垃圾邮件过滤器spam filter** ——

防止人们用随机提案淹没系统而没有后果。

 如果您的提案获胜并执行，您将取回押金。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 如果您的提案失败，押金就会丢失或被锁定——从而阻止低质量或巨魔提案。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## **（5）活动 — 广播重要时刻**Events — Broadcasting Important Moments

在真正的 DAO 中，仅仅将信息存储在链上是不够的。

你还想向外界**广播重要的里程碑** ——

以便前端、应用程序、资源管理器和用户可以**监听** 、 **做出反应**和**显示**最新更新。

这正是**事件**的作用。

您可以将它们视为合同每次发生关键事件时发出**的响亮公告** 。

### **完整活动列表**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalCreated(uint256 id, string description, address proposer, uint256 depositAmount);
event Voted(uint256 proposalId, address voter, bool support, uint256 weight);
event ProposalExecuted(uint256 id, bool passed);
event QuorumNotMet(uint256 id, uint256 votesTotal, uint256 quorumNeeded);
event ProposalDepositPaid(address proposer, uint256 amount);
event ProposalDepositRefunded(address proposer, uint256 amount);
event TimelockSet(uint256 duration);
event ProposalTimelockStarted(uint256 proposalId, uint256 executionTime);
```

### **深入探讨：每个事件的含义**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **提案创建**`ProposalCreated`

[🧱](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalCreated(uint256 id, string description, address proposer, uint256 depositAmount);
```

每当创建新提案时，

此活动启动 — 宣布：

提案的 **ID**

简**短**描述其内容

提议者的**地址**

他们锁定了多少**存款**来创建它

前端使用它来**在创建新提案时实时列出它们** 。 

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **投票**`Voted`

[🗳️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event Voted(uint256 proposalId, address voter, bool support, uint256 weight);
```

每当有人投票时，

本次活动直播：

他们投票支持哪个**提案**

哪个**地址**投票了

无论他们投**了赞成**票还是**反对**票

他们使用了多少**投票权** （代币权重）

 前端可以显示基于此事件的**实时投票更新** ！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **提案已执行**`ProposalExecuted`

[🏁](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalExecuted(uint256 id, bool passed);
```

当提案最终执行时——

无论通过还是失败——

这个事件火了，让大家知道：

哪项**提案**已最终确定

是**通过**  （true） 还是**失败**  （false）

 帮助跟踪哪些提案实际**完成** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **法定人数未满足**`QuorumNotMet`

[📉](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event QuorumNotMet(uint256 id, uint256 votesTotal, uint256 quorumNeeded);
```

如果提案因参与人数不足（未达到法定人数）而**失败** ，

这个事件确切地解释了：

哪个**提案**失败了

收集了多少**票**

需要多少**票**才能达到法定人数

 对于分析选民投票率和 DAO 健康状况非常有用！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **提案定金已付**`ProposalDepositPaid`

[💰](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalDepositPaid(address proposer, uint256 amount);
```

当有人提交提案并支付押金时，

此事件记录：

**提议者地址**

锁定的代币**数量**

 前端可以显示存款跟踪和承诺证明。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **提案定金已退还**`ProposalDepositRefunded`

[🎁](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalDepositRefunded(address proposer, uint256 amount);
```

如果提案**通过并执行** ，

提议者**拿回他们的押金** ——

并且此事件记录了退款的发生。

 有助于证明获胜的提议者会得到奖励。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **时间锁集**`TimelockSet`

[⏰](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event TimelockSet(uint256 duration);
```

当管理员**更改时间锁持续时间** （从传递到执行之间的等待时间）时，

此事件宣布**新的时间锁时间** 。

 保持治理透明 — 没有隐藏的参数更改。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **提案时间锁已开始**`ProposalTimelockStarted`

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event ProposalTimelockStarted(uint256 proposalId, uint256 executionTime);
```

当提案**赢得投票**但在执行前进入**时间锁延迟**时，

本次活动宣布：

哪个**提案**正在进入时间锁

它何时有**资格执行**

 对于显示提案何时处于等待区非常重要。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## **（6）仅限管理员访问**Admin-Only Access**（最后一次！**

[🛡️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
modifier onlyAdmin() {
    require(msg.sender == admin, "Only admin can call this");
    _;
}
```

这是我们经典的 **onlyAdmin** 修饰符。

它只是**检查**调用某个函数的人是否是**管理员** ——

部署合约的人。

如果您不是管理员，则呼叫**将恢复**并出现错误。

如果您是管理员，则该函数将继续正常运行（_ 表示“继续该函数”）。

 我们使用它来**保护敏感设置**  —

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

比如更改法定人数百分比或更新时间锁持续时间——

这样随机用户就无法弄乱治理规则。

## **（7）构造函数 — 设置 DAO 的核心设置**Constructor — Setting Up the DAO's Core Settings

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

在我们的治理系统开始运行之前，

我们需要**设置一些起始参数 starting parameters**——

例如我们将使用哪种代币进行投票、投票开放多长时间以及第一个管理员是谁。

构造函数在**部署合约时**处理所有这些问题。

它看起来像这样：

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
constructor(address _governanceToken, uint256 _votingDuration, uint256 _timelockDuration) {
    governanceToken = IERC20(_governanceToken);
    votingDuration = _votingDuration;
    timelockDuration = _timelockDuration;
    admin = msg.sender;
    emit TimelockSet(_timelockDuration);
}
```

## **逐行细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
governanceToken = IERC20(_governanceToken);
```

当合约部署时，

我们希望部署者**告诉我们**将用于治理投票的 ERC-20 代币的地址。

我们将该代币的地址保存在合约中，以便：

我们知道在哪里检查人们的余额。

我们知道哪个代币赋予用户投票权。

 这锁定了**哪个代币控制了 DAO**。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

votingDuration = _votingDuration;

这设置了每个提案将保持开放投票**的秒数** 。

创建提案  后，它将在经过这么长时间后自动**过期** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 确保投票期不是无止境的。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
timelockDuration = _timelockDuration;
```

这设置了**强制等待时间**

在提案*获胜*和实际**执行**之间。

 这通过让用户有时间在最终确定之前退出或对重大更改做出反应来保护用户。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
admin = msg.sender;
```

部署合同的人员将成为**管理员** 。

 管理员可以稍后调整治理设置（如仲裁百分比、存款规模、时间锁持续时间）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
emit TimelockSet(_timelockDuration);
```

合约一部署，

我们**发出一个事件** ，向世界宣布时间锁设置。

 这使一切**保持透明** ——

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

任何关注区块链的人都会立即知道起始时间锁是什么。

## ****

## **（8）更新仲裁百分比**Updating the Quorum Percentage

[🛠️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function setQuorumPercentage(uint256 _quorumPercentage) external onlyAdmin {
    require(_quorumPercentage <= 100, "Quorum percentage must be between 0 and 100");
    quorumPercentage = _quorumPercentage;
}
```

### **这是怎么回事？**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此功能允许**管理员**更改**仲裁百分比**  —

这是必须投票才能使提案有效的代币**总数的最小百分比** 。

 只有管理员可以调用此函数（感谢 `onlyAdmin` 修饰符）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 新法定人数必须**介于 0% 到 100% 之间** ——

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

否则，它会恢复并出现错误。

设置后，新的 `quorumPercentage` 将应用于**所有未来的提案** 。

## **（9） 更新提案存款金额**Updating the Proposal Deposit Amount

[💰](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function setProposalDepositAmount(uint256 _proposalDepositAmount) external onlyAdmin {
    proposalDepositAmount = _proposalDepositAmount;
}
```

### **这是 怎么回事？**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此功能允许**管理员**更改用户必须存入的**代币数量**才能创建提案。

 只有管理员可以调用此命令（因为 `onlyAdmin` 修饰符）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 这里没有范围检查——管理员应该根据 DAO 的需求负责任地设置它。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

如果存款太小，垃圾邮件提案可能会淹没系统。

如果太高，可能会阻碍参与。

## **（10）更新时间锁持续时间** Updating the Timelock Duration

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function setTimelockDuration(uint256 _timelockDuration) external onlyAdmin {
    timelockDuration = _timelockDuration;
    emit TimelockSet(_timelockDuration);
}
```

### **这是怎么回事？**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此功能允许**管理员**更新**时间锁持续时间**  —

提案通过和执行之间的等待期。

 只有管理员可以调用它（再次使用 `onlyAdmin` 修饰符）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

设置新的时间锁  后，它**会发出一个事件** （`TimelockSet`）来公开宣布更改。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这使一切都**保持透明** ，因此用户知道治理过程是加快还是减慢。

### **为什么这很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

时间锁让社区有时间在提案通过后**做出反应** 。

如果时间锁太短，决策可能会让人感觉仓促。

如果时间太长，执行可能会令人沮丧地延迟。

 能够调整它使 DAO 在成长和发展过程中保持**灵活性** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## （11）**创建提案 — 让我们做出一些决定！**Creating a Proposal — Let’s Make Some Decisions!

在去中心化的治理体系中，

**一切都从提案开始** 。

无论是建议新功能、升级合同还是更改设置——

当**有人提出一个想法**并要求社区投票时，这一切都始于。

这个函数 `createProposal（）` 是用户正式向 DAO  **提交提案**的地方——锁定存款，定义如果提案获胜应采取的行动，并设定投票截止日期。

让我们来看看它是如何工作的。

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function createProposal(
    string calldata _description,
    address[] calldata _targets,
    bytes[] calldata _calldatas
) external returns (uint256) {
    require(governanceToken.balanceOf(msg.sender) >= proposalDepositAmount, "Insufficient tokens for deposit");
    require(_targets.length == _calldatas.length, "Targets and calldatas length mismatch");

    governanceToken.transferFrom(msg.sender, address(this), proposalDepositAmount);
    emit ProposalDepositPaid(msg.sender, proposalDepositAmount);

    proposals[nextProposalId] = Proposal({
        id: nextProposalId,
        description: _description,
        deadline: block.timestamp + votingDuration,
        votesFor: 0,
        votesAgainst: 0,
        executed: false,
        proposer: msg.sender,
        executionData: _calldatas,
        executionTargets: _targets,
        executionTime: 0
    });

    emit ProposalCreated(nextProposalId, _description, msg.sender, proposalDepositAmount);

    nextProposalId++;
    return nextProposalId - 1;
}
```

### **分步细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **需要什么输入？**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

`string calldata _description` — 描述提案内容的简短文本。

`address[] calldata _targets` — 该提案将与之交互的合约地址（如果通过）。

`bytes[] calldata _calldatas` — 将发送到每个目标的实际函数调用数据（例如打包成字节的函数调用）。

 

这些输入定义了**提案的含义**以及如果获得批准后**将做什么** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 1 步：检查用户是否有足够的令牌**

[🛡️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(governanceToken.balanceOf(msg.sender) >= proposalDepositAmount, "Insufficient tokens for deposit");
```

在某人可以创建提案之前，

他们必须**至少**有足够的治理代币来支付**提案押金** 。

 这可以防止垃圾邮件——人们需要真正的“游戏皮肤”来提出一些东西。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2 步：确保目标和调用数据匹配**

[📏](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(_targets.length == _calldatas.length, "Targets and calldatas length mismatch");
```

每个目标合约必须有一个**相应的函数调用** （calldata）。

我们检查以确保列表的**长度相同** 。

 这可以防止以后出现执行错误。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：收取提案押金**

[💸](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
governanceToken.transferFrom(msg.sender, address(this), proposalDepositAmount);
emit ProposalDepositPaid(msg.sender, proposalDepositAmount);
```

我们将提议者的存款代币**转移到**  DAO 合约中。

然后我们**发出一个事件** ，宣布付款。

 现在系统知道提议者是认真的。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4 步：保存新提案**

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
proposals[nextProposalId] = Proposal({...});
```

我们创建一个新的 `Proposal` 结构体，并填充：

其唯一 ID （`nextProposalId`）

用户提供的描述

截止日期（现在 + 投票持续时间）

最初零票

提议者地址

目标地址和呼叫数据列表

尚无执行时间（如果提案获胜，则稍后设置）

 这正式将提案存储在我们 DAO 的内存中。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 5  步：宣布新提案**

[📢](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
emit ProposalCreated(nextProposalId, _description, msg.sender, proposalDepositAmount);
```

我们触发一个事件，以便前端和用户**可以看到**已创建新提案。

### **第 6 步：更新提案计数器**

[🆔](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
nextProposalId++;
```

我们递增计数器，因此下一个提案将获得一个新的唯一 ID。

最后，该函数返回新创建的提案的 **ID**。

 现在该提案已上线，可供社区开始投票！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **为什么这个函数很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此功能是治理的**开始** 。

它确保：

只有认真的用户才能提出更改。

提案与实际行动（而不仅仅是想法）适当地联系在一起。

一切都记录在链上，可见且透明。

 没有这个功能，就没有可以投票的决定！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这是**社区驱动控制community-driven control**的第一步。

## ****

## **（12）投票——在 DAO 中发表你的意见**Casting Your Vote — Have Your Say in the DAO

[🗳️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在用户可以创建提案，

下一步是**让社区有发言权** 。

这个函数 `vote（）` 是用户正式**投票**的地方——

支持或反对某项提案——

他们的投票权基于他们持有的治理代币数量。

投票是想法转化为决策的方式。

让我们深入了解它是如何工作的。

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function vote(uint256 proposalId, bool support) external {
    Proposal storage proposal = proposals[proposalId];
    require(block.timestamp < proposal.deadline, "Voting period over");
    require(governanceToken.balanceOf(msg.sender) > 0, "No governance tokens");
    require(!hasVoted[proposalId][msg.sender], "Already voted");

    uint256 weight = governanceToken.balanceOf(msg.sender);

    if (support) {
        proposal.votesFor += weight;
    } else {
        proposal.votesAgainst += weight;
    }

    hasVoted[proposalId][msg.sender] = true;

    emit Voted(proposalId, msg.sender, support, weight);
}
```

### **分步细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 1 步：加载提案**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
Proposal storage proposal = proposals[proposalId];
```

我们首先**加载**用户想要投票的提案，

使用提案的 ID。

 现在，我们拥有触手可及的所有信息（截止日期、投票、提议者等）。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2 步：确保投票仍然开放**

[🛑](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(block.timestamp < proposal.deadline, "Voting period over");
```

仅允许在提案截止日期**之前**进行投票。

如果投票窗口已关闭，则函数**将恢复** 。

 这确保了选票仅在正式投票期间计算。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：确保用户持有治理代币**

[🪙](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(governanceToken.balanceOf(msg.sender) > 0, "No governance tokens");
```

如果您不持有任何治理代币，

你没有机会投票。

 这保持了投票的公平性——只有真正的利益相关者才能参与。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4 步：防止重复投票**

[🚫](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(!hasVoted[proposalId][msg.sender], "Already voted");
```

每个地址每个提案**只能投票一次** 。

 没有垃圾邮件，以后不会改变主意。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

一旦你投票，它就被锁定了！

### **第 5  步：计算投票权**

[⚖️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 weight = governanceToken.balanceOf(msg.sender);
```

您拥有**的代币越多** ，您的投票**就越重要** 。

 更大的代币持有者具有更大的影响力。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

（这就是为什么治理代币很有价值——它们代表着真正的权力。

### **第 6  步：计票**

[🗳️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
if (support) {
    proposal.votesFor += weight;
} else {
    proposal.votesAgainst += weight;
}
```

根据用户是支持还是反对该提案：

我们将他们的权重添加到 `votesFor` 中

或者我们将其添加到 `votesAgainst` 中

 每一次投票都会改变平衡！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 7 步：将用户标记为已投票**

[📝](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
hasVoted[proposalId][msg.sender] = true;
```

我们记录该用户**现已**对该提案进行了投票。

他们不能再投票了。

 保持一切清洁和防篡改。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 8 步：发出投票事件**

[📢](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
emit Voted(proposalId, msg.sender, support, weight);
```

最后，我们**播报一个事件** ——

告诉外界谁投票了，投票了哪个提案，权力有多大。

 前端可以实时更新投票统计！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **为什么这个函数很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这一职能是**治理的命脉** 。

   没有它，提案就会积满灰尘。

   它使投票具有加权、公平、安全和可审计性——

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

         这正是真正的 DAO 运作所需要的。

         它确保每个选民都有**一个与实际利害关系**相关的声音。

## **（13）敲定提案——决定命运**Finalizing a Proposal — Deciding the Fate

好吧——

投票结束了。

社区已经发声了。

现在最大的问题是：

**提案通过了吗？**

**失败了吗？**

**是否有足够的参与甚至使其有效？**

这个函数 `finalizeProposal（）` 是 DAO 检查最终结果的地方——

并将提案移至执行阶段（如果通过）或将其标记为失败（如果未通过）。

让我们来看看。

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function finalizeProposal(uint256 proposalId) external {
    Proposal storage proposal = proposals[proposalId];
    require(block.timestamp >= proposal.deadline, "Voting period not yet over");
    require(!proposal.executed, "Proposal already executed");
    require(proposal.executionTime == 0, "Execution time already set");

    uint256 totalSupply = governanceToken.totalSupply();
    uint256 totalVotes = proposal.votesFor + proposal.votesAgainst;
    uint256 quorumNeeded = (totalSupply * quorumPercentage) / 100;

    if (totalVotes >= quorumNeeded && proposal.votesFor > proposal.votesAgainst) {
        proposal.executionTime = block.timestamp + timelockDuration;
        emit ProposalTimelockStarted(proposalId, proposal.executionTime);
    } else {
        proposal.executed = true;
        emit ProposalExecuted(proposalId, false);
        if (totalVotes < quorumNeeded) {
            emit QuorumNotMet(proposalId, totalVotes, quorumNeeded);
        }
        // Deposit is NOT refunded here for failed proposals.
    }
}
```

### **分步细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 1 步：加载提案**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
Proposal storage proposal = proposals[proposalId];
```

我们拉出用户想要最终确定的提案。

 现在我们可以访问其全部详细信息。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2  步：确保投票结束**

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(block.timestamp >= proposal.deadline, "Voting period not yet over");
```

我们**必须等到**正式投票期结束。

 这可以防止某人在投票仍在进行时进行最终确定。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：确保尚未最终确定**

[🚫](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(!proposal.executed, "Proposal already executed");
```

如果提案已经执行或最终确定，

我们**阻止**调用以避免双重终结。

 防止事故和重播。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4 步：确保它尚未进入时间锁**

[🕰️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(proposal.executionTime == 0, "Execution time already set");
```

如果提案已经进入“等待执行”阶段，

没有必要再次敲定。

 这使得逻辑干净且防弹。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 5 步：计算法定人数和总票数**

[📈](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 totalSupply = governanceToken.totalSupply();
uint256 totalVotes = proposal.votesFor + proposal.votesAgainst;
uint256 quorumNeeded = (totalSupply * quorumPercentage) / 100;
```

我们现在检查：

**总共有多少个代币** （totalSupply）

**投了多少票** （totalVotes）

**需要多少票**数（法定人数）

 这就是我们验证是否有足够多的人参与的方式。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **步骤 6A：如果提案获得通过并达到法定人数**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
if (totalVotes >= quorumNeeded && proposal.votesFor > proposal.votesAgainst) {
    proposal.executionTime = block.timestamp + timelockDuration;
    emit ProposalTimelockStarted(proposalId, proposal.executionTime);
}
```

如果有足够多的人投票 **，** 并且**投票赞成**的人多于**反对**的人......

然后，提案**进入时间锁定期** 。

**executionTime** 设置为 **now + timelockDuration**，

这意味着它只有在等待期后才能执行。

 这使社区有时间为变更做准备或在需要时做出响应。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 我们发出一个事件，以便每个人都知道时间锁何时开始以及何时允许执行。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **步骤 6B：如果提案失败**

[❌](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
else {
    proposal.executed = true;
    emit ProposalExecuted(proposalId, false);
    if (totalVotes < quorumNeeded) {
        emit QuorumNotMet(proposalId, totalVotes, quorumNeeded);
    }
}
```

如果提案**失败** （没有足够的票数，或者有更多的反对票）：

我们立即将其标记为**已执行** （不会发生进一步的作）。

发出一个事件，说提案失败了。

如果问题在于**没有足够的选民参与** ，

我们发出一个额外的事件 `QuorumNotMet` 来解释它失败的确切原因。

 这使治理过程**保持透明和可审计** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **为什么这个函数很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这是关键检查点，其中：

社区决定得到尊重。

垃圾邮件和低参与度提案将被过滤掉。

获胜提案被**锁定**并准备安全执行。

 如果没有这一步，投票将毫无意义。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 这就是将选票转化为**实际后果**的原因。

## **（14）执行提案——使决策成为现实**Executing a Proposal — Making the Decision Real

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

所以，社区已经投票了。

该提案获得通过。

时间锁定期结束。

现在是合同做**社区决定的时候**了——

无论是转移资金、调用其他合约上的函数，还是升级系统的某些部分。

这个函数 `executeProposal（）` 是 DAO  **实际执行**选民意愿的地方。

让我们来看看。

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function executeProposal(uint256 proposalId) external nonReentrant {
    Proposal storage proposal = proposals[proposalId];
    require(!proposal.executed, "Proposal already executed");
    require(proposal.executionTime > 0 && block.timestamp >= proposal.executionTime, "Timelock not yet expired");

    proposal.executed = true; // set executed early to prevent reentrancy

    bool passed = proposal.votesFor > proposal.votesAgainst;

    if (passed) {
        for (uint256 i = 0; i < proposal.executionTargets.length; i++) {
            (bool success, bytes memory returnData) = proposal.executionTargets[i].call(proposal.executionData[i]);
            require(success, string(returnData));
        }
        emit ProposalExecuted(proposalId, true);
        governanceToken.transfer(proposal.proposer, proposalDepositAmount);
        emit ProposalDepositRefunded(proposal.proposer, proposalDepositAmount);
    } else {
        emit ProposalExecuted(proposalId, false);
        // Deposit is NOT refunded here for failed proposals; it was not refunded in finalizeProposal either.
    }
}
```

### **分步细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 1 步：加载提案**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
Proposal storage proposal = proposals[proposalId];
```

我们首先使用提案的 ID 提取提案数据。

 现在我们拥有检查它是否准备好执行所需的所有详细信息。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2 步：确保它尚未执行**

[🚫](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(!proposal.executed, "Proposal already executed");
```

如果有人尝试执行**两次**提案，

此检查**会立即阻止**它们。

 双重执行 = 避免灾难。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：确保时间锁结束**

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(proposal.executionTime > 0 && block.timestamp >= proposal.executionTime, "Timelock not yet expired");
```

必须有两件事是正确的：

**必须**设置时间锁执行时间（意味着提案通过投票）。

**当前时间**必须在执行时间**之后** 。

 这确保用户不会在社区有时间做出反应之前匆忙通过提案。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4 步：在执行任何其他作*之前*将提案标记为已执行**

[🔒](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
proposal.executed = true; // set executed early to prevent reentrancy
```

这是一个**非常重要的安全举措** 。

在调用任何外部合约之前，我们将**提案标记为已执行** ——

这样，如果发生奇怪的事情，提案就无法通过重入重新执行或攻击。

 这遵循了 Solidity 安全合约设计的最佳实践。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 5 步：检查提案是否实际通过**

[⚖️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
bool passed = proposal.votesFor > proposal.votesAgainst;
```

即使我们在这里，

我们**仔细检查**社区是否真的批准了该提案。

 只有通过的提案才能实际执行作。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 6A 步：执行提案（如果通过）**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
if (passed) {
    for (uint256 i = 0; i < proposal.executionTargets.length; i++) {
        (bool success, bytes memory returnData) = proposal.executionTargets[i].call(proposal.executionData[i]);
        require(success, string(returnData));
    }
    emit ProposalExecuted(proposalId, true);
    governanceToken.transfer(proposal.proposer, proposalDepositAmount);
    emit ProposalDepositRefunded(proposal.proposer, proposalDepositAmount);
}
```

如果提案获得通过：

我们**循环遍历**所有目标合约。

我们使用存储的 calldata 在每个目标上**调用**相应的函数。

如果任何调用**失败** ，则整个执行**将恢复** （以保持一致和安全）。

 这是真正发生变化的地方——升级、转移、新设置，以及提案想要做的任何事情！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

成功执行后：

我们**发出一个事件** ，将提案标记为已通过并已执行。

我们**退还**提议者的代币押金（因为他们贡献了一个成功的想法）。

 好的提案有回报！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **步骤 6B：如果提案失败**

[❌](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
else {
    emit ProposalExecuted(proposalId, false);
}
```

如果提案失败（目前很少见，但仍然有可能），

我们只是**发出一个事件** ，说它失败了。

在这种情况下，押金  不予退还——它保留在合同中，作为对失败想法的惩罚。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **为什么这个函数很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这是将**选票转化为实际行动**的**最后一步** 。

 它尊重社区做出的决定。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

       它自动执行——不需要可信的中间人。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

       它会自动处理对好提案的奖励（退款）和对坏提案的惩罚。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

       它使整个过程**安全** 、 **透明**且**无需信任** 。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## **（15）检查提案结果——它是通过还是失败？**Checking Proposal Results — Did It Pass or Fail?

提案最终确定并执行后，

用户可能希望**检查结果** ，而无需手动挖掘事件。

这个函数 `getProposalResult（）` 给出了一个**简单的摘要** ：

提案是否**通过** 、 **失败**或**未达到法定人数** 。

让我们看看它是如何工作的。

### **完整代码**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function getProposalResult(uint256 proposalId) external view returns (string memory) {
    Proposal storage proposal = proposals[proposalId];
    require(proposal.executed, "Proposal not yet executed");

    uint256 totalSupply = governanceToken.totalSupply();
    uint256 totalVotes = proposal.votesFor + proposal.votesAgainst;
    uint256 quorumNeeded = (totalSupply * quorumPercentage) / 100;

    if (totalVotes < quorumNeeded) {
        return "Proposal FAILED - Quorum not met";
    } else if (proposal.votesFor > proposal.votesAgainst) {
        return "Proposal PASSED";
    } else {
        return "Proposal REJECTED";
    }
}
```

### **分步细分**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 1 步：加载提案**

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
Proposal storage proposal = proposals[proposalId];
```

我们按其 ID 提取提案详细信息。

 现在我们可以访问其投票结果和截止日期信息。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2 步：确保它已经执行**

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(proposal.executed, "Proposal not yet executed");
```

我们只允许在提案最终确定并标记为已执行**后检查**结果。

 这可以防止人们在**投票仍在进行时**询问结果。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：计算所需的总票数和法定人数**

[🧮](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
uint256 totalSupply = governanceToken.totalSupply();
uint256 totalVotes = proposal.votesFor + proposal.votesAgainst;
uint256 quorumNeeded = (totalSupply * quorumPercentage) / 100;
```

**totalSupply**：总共存在多少治理代币。

**totalVotes**：该提案实际投了多少票。

**quorumNeeded**：提案需要多少票才能被视为有效。

 这些数字有助于确定该提案是否合法。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4A 步：如果参与不足**

[🛑](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
if (totalVotes < quorumNeeded) {
    return "Proposal FAILED - Quorum not met";
}
```

如果没有投出足够的票数（未达到法定人数），

该提案自动**失败** ，即使有更多的“赞成”票。

 防止一小群人不公平地控制系统。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **步骤 4B：如果通过**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
else if (proposal.votesFor > proposal.votesAgainst) {
    return "Proposal PASSED";
}
```

如果**赞成**票多于**反对**票，

并达到法定人数，提案顺利**通过** 。

 准备执行作！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4C 步：如果被拒绝**

[❌](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
else {
    return "Proposal REJECTED";
}
```

如果**反对**票多于**赞成**票，

该提案被**拒绝** 。

不会对系统进行  任何更改。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **为什么这个函数很重要**

[🌟](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此函数提供：

 一个**简单易读的答案** ，回答“这个提案发生了什么？

[📜](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 保证只能检查**已完成的提案** 。

[🚫](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 透明度，无需用户手动挖掘区块链事件。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 对于希望了解治理结果的前端、浏览器和用户非常有帮助。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

## **（16）查看完整的提案详细信息**Viewing Full Proposal Details

[📋](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function getProposalDetails(uint256 proposalId) external view returns (Proposal memory) {
    return proposals[proposalId];
}
```

### **这是 怎么回事？**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

此功能允许**任何人**查找有关特定提案的完整**信息** 。

您传入 proposalId。

合约返回**整个 Proposal 结构体**  —

包括其描述、投票、提议者、目标、执行数据等。

 没有复杂的逻辑。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 没有限制。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 只是一种简单、公开的方式来查看有关提案的所有内容。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### Remix实战步骤

### **第 1 步：部署治理令牌**

首先，我们需要一个代表**投票权**的**代币** 。

为此，您可以使用简单的 ERC-20 代币。

下面是可以快速部署的**最小令牌** ：

```jsx
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract GovernanceToken is ERC20 {
    constructor() ERC20("GovernanceToken", "GT") {
        _mint(msg.sender, 1000000 * 10 ** decimals()); // Mint 1 million tokens to yourself
    }
}
```

 首先部署此 `GovernanceToken` 合约。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

部署后，您的钱包中将有一堆代币。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 2 步：部署治理合约**

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在部署 `DecentralizedGovernance` 合约。

**所需的构造函数参数：**

`governanceToken` 地址 — 您刚刚部署的 `GovernanceToken` 的地址。

`votingDuration` — 提案开放投票的时间（例如：`600` 秒 10 分钟）。

`timelockDuration` — 提案通过后等待多长时间才能执行（例如：`300` 秒 5 分钟）。

 使用这些值部署它，您的治理系统就会上线！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 3 步：批准治理合约以使用您的代币**

[🔓](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

在创建提案之前，您需要**批准**治理合约以**使用您的代币**进行存款。

转到 Remix 中的 `GovernanceToken` 合约并调用：

```jsx
approve(governanceContractAddress, amount)
```

其中：

`governanceContractAddress` = 你的 DAO 合约地址

`amount` = 足够的代币来支付 `proposalDepositAmount` 以及稍后的投票费（例如：批准 100 个代币）。

 如果没有这一步，您的提案将失败，因为合约无法提取您的代币！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 4 步：创建一个有趣的目标合约以供稍后执行**

[🎯](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在为了让演示更加**精彩** ，

让我们创建一个 **FunTransfer 合约**  —

如果提案通过，这将使我们**能够移动以太币** ！

这是一个超级简单的：

```jsx
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

contract FunTransfer {
    address public owner;
    uint256 public received;

    constructor() {
        owner = msg.sender;
    }

    function receiveEther() external payable {
        received += msg.value;
    }

    function withdrawEther() external {
        payable(owner).transfer(address(this).balance);
    }

    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```

 也部署这个 `FunTransfer` 合约。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 您甚至可以手动向它发送一些以太币（通过 Remix UI），以便它保持平衡。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 5 步：创建提案**

[📝](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在我们创建一个提案，该**提案**将触发对 `FunTransfer` 合约的作。

您需要：

以 `FunTransfer` 合约的地址为**目标** 。

**Calldata** 来调用类似 `withdrawEther（）` 的函数。

如何轻松获取调用数据：

在 Remix 中创建新的 script.js 文件

粘贴以下代码：

```jsx
const { ethers } = require('ethers');

const abi = [
  "function withdrawEther()"
];

const iface = new ethers.utils.Interface(abi);
const data = iface.encodeFunctionData("withdrawEther");

console.log(data);
```

运行脚本

复制该字节输出。

现在通过调用以下命令创建提案：

```jsx
createProposal(
    "Proposal to withdraw Ether from FunTransfer",
    ["0xd8b8A44ce1fB552f4A4dE0cb074EeBf5A24a2..."],
    ["0x736237.."]
)
```

 治理合约将存储您的提案。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 它还将扣除您的押金！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 6  步：对提案进行投票**

[🗳️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在， **要投票** ，

只需调用：

```jsx
vote(proposalId, true)  // Vote FOR
```

 您需要使用拥有治理代币的地址进行投票。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 请记住，您拥有的代币越多，您的投票权重就越大！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 7  步：等待投票结束并最终确定**

[⏳](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

等待 **votingDuration** 到期。

然后调用：

```jsx
finalizeProposal(proposalId)
```

 这将检查它是否通过、满足仲裁，并计划执行。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

 它将在 `timelockDuration` 之后设置一个 `executionTime`。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **第 8 步：等待时间锁并执行！**

[🛡️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

时间锁到期后，调用：

```jsx
executeProposal(proposalId)
```

 如果提案获得通过，

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

治理合约将在 `FunTransfer` 上调用 `withdrawEther（）` 函数。

 以太币将转入所有者钱包！

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

提案创建  者也将退还他们的押金。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### 简化步骤参考

### 步骤1: 准备治理代币

1. 部署ERC-20治理代币（如GOV）
2. 给多个账户分发代币
3. 确保代币分布合理

### 步骤2: 部署DAO合约

1. 设置投票期（如3600秒）
2. 设置时间锁（如1800秒）
3. 设置法定人数（如20%）
4. 设置提案押金（如100个代币）

### 步骤3: 创建提案

1. 批准DAO合约使用治理代币
2. 调用`createProposal("增加开发预算")`
3. 检查提案ID和详情
4. 验证押金被扣除

### 步骤4: 投票测试

1. 使用不同账户调用`vote(0, true)`
2. 观察投票权重和累积票数
3. 测试重复投票保护
4. 确保达到法定人数

### 步骤5: 执行流程

1. 投票期结束后调用`finalizeProposal(0)`
2. 观察提案进入时间锁状态
3. 等待时间锁结束
4. 调用`executeProposal(0)`完成执行

# 3、下一步

**尝试以下改进:**

1. 实现委托投票机制
2. 添加提案类别和不同投票规则
3. 实现链上提案执行（调用其他合约）
4. 添加紧急暂停功能
5. 实现多签管理员机制
6. 创建DAO财库管理

### 📚 扩展知识

**委托投票实现**

```solidity
mapping(address => address) public delegates;

function delegate(address delegatee) external {
    delegates[msg.sender] = delegatee;
    emit DelegateChanged(msg.sender, delegatee);
}

function getVotingPower(address voter) public view returns (uint256) {
    uint256 power = governanceToken.balanceOf(voter);

    // 加上委托给该地址的投票权
    // 这里需要遍历或使用更复杂的数据结构
    return power;
}
```

委托投票允许代币持有者将投票权委托给专业的治理参与者。