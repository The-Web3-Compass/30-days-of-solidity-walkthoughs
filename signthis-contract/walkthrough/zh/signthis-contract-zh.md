# 签名验证

Day: Day 19
ID: 19
原文: https://builder-hub.notion.site/SignThis-Contract-1da5720a23ef80d69ae9c1559ba48a82?pvs=25
状态: 完成
译者: 雷雷
难度等级: 中级

# 学习内容

好吧——到目前为止，我们在 Solidity 之旅中取得了一些重大进展。
我们学会了如何：
      

- 分配**所有权**
- 使用**修饰符**保护函数
- 使用**映射**来存储用户数据
- 进行全面的**代币销售**
- 使用**委托调用**构建基于插件的模块化系统

但现在，是时候将**所有这些技能付诸行动**并解决现实世界的问题了——这是每个 Web3 构建者都能与之相关的。

**场景  ：私人 Web3 事件**
你正在组织代币**门控会议**、**创始人聚会**或**链上研讨会**。只有选定的客人才能进入。
现在挑战来了......

**传统方法：**
你将每个与会者的地址存储在链上。
你可以在办理登机手续时手动检查每一项。
每次地址更新或拼写错误都会消耗 gas。
💢这很笨拙。太贵了。那不是 Web3。

**✅更智能的方式：链下签名**
而不是上传一大堆地址......
如果：

- 活动组织者为每位获批的来宾签署一条消息
- 客人携带已签名的邀请函
- 合约只是在签到时验证签名
- 无需在链上存储任何白名单

是的，这一切都是使用以太坊内置的 `ecrecover` 功能安全地完成的。

**我们的游戏计划🌌**
 以下是我们将要构建的内容，以及我们将如何分解它：
        **1. 活动设置**
        组织者定义姓名、日期和最大与会者人数。
       **2.加密邀请**
       后端不是存储地址，而是为每个已批准的来宾签署一条消息。
       **3.签到流程**
       来宾提交他们签名的消息。
       该合同使用 `ecrecover` 来验证它是否由组织者签署。
      **4.安全性和灵活性**
        链上没有地址列表。没有预加载的白名单。没有浪费的气体。
         只有有效的、签名的与会者才能进入——而且这一切都可以在链上完全验证。

**您将学到什么**
如何对结构化数据进行哈希处理  （`abi.encodePacked`）
为什么以太坊使用签名消息前缀
`ecrecover（）` 如何让你在链上验证链下批准
如何实施当今生产中实际使用的轻量级、节能的访问系统

由此可见，在传统的活动管理系统中，身份验证往往依赖于中心化的数据库和复杂的权限控制机制。参与者需要注册账户、记住密码，组织者需要维护用户列表、处理忘记密码等繁琐事务。这种模式不仅用户体验复杂，还存在单点故障和数据泄露的风险。

**区块链技术为我们提供了一种全新的思路**：通过密码学证明而非中心化授权来验证身份。本「签名验证活动合约」正是这一理念的实践——利用数字签名技术，构建了一个去中心化、安全高效的活动管理系统。

### **🎯 设计哲学**

本合约的核心创新在于 **"链下签名，链上验证"** 的模式：

- **组织者**在链下为合法参与者生成数字签名
- **参与者**提交签名到区块链合约进行验证
- **智能合约**通过`ecrecover`函数恢复签名者地址，确认身份有效性

这种方法带来了多重优势：

- **零Gas预注册**：参与者无需预先支付Gas费注册
- **无限扩展性**：签名生成不受区块链吞吐量限制
- **隐私保护**：参与者名单不会全部暴露在链上
- **成本优化**：批量签到大幅降低Gas消耗

**学习目标**：掌握数字签名验证和链下认证

**核心技能**：ecrecover、消息哈希、签名生成

**完整代码**：‣ 

# 1、产品需求书

### **a.用户路径图**

**组织者路径organizer**

1. **合约部署**
    - 调用构造函数，传入活动参数
    - 自动成为organizer，获得管理权限
2. **签名生成**
    - 获取参与者钱包地址
    - 调用getMessageHash获取消息哈希
    - 使用私钥对哈希进行签名
    - 分离签名为v, r, s组件
3. **批量管理**
    - 为多个参与者生成签名
    - 使用batchCheckIn进行批量签到
    - 监控活动状态和参与人数
    

**参与者路径Attendee**

1. **活动发现**
    - 查看活动基本信息
    - 确认活动状态(isEventActive)
2. **签到流程**
    - 从组织者获取签名数据
    - 提交v, r, s参数到合约
    - 等待签名验证和状态更新
3. **状态确认**
    - 验证hasAttended状态
    - 查看签到历史记录

![image.png](%E7%AD%BE%E5%90%8D%E9%AA%8C%E8%AF%81%2028c06421268880a1a3b5d1d75cf53398/image.png)

### **b.数据库结构**

**1.合约基本信息（Contract Basic Information）**

| **Contract / 合约** | **Type / 类型** | **Bases / 基础合约** | **含义 / Description** |
| --- | --- | --- | --- |
| SignThis | Implementation / 实现合约 | None / 无 | 签名验证活动合约 / Signature Verification Event Contract |

**2.状态变量表 (State Variables Table)**

| **L** | **Field Name / 字段名** | **Visibility / 可见性** | **Mutability / 可变性** | **Data Type / 数据类型** | **Description / 描述** |
| --- | --- | --- | --- | --- | --- |
| L | eventName | Public / 公开 | view / 只读 | string / 字符串 | 活动名称 / Event Name |
| L | organizer | Public / 公开 | view / 只读 | address / 地址 | 组织者钱包地址 / Organizer Wallet Address |
| L | eventDate | Public / 公开 | view / 只读 | uint256 / 无符号整数 | 活动日期时间戳 / Event Date Timestamp |
| L | maxAttendees | Public / 公开 | view / 只读 | uint256 / 无符号整数 | 最大参与人数 / Maximum Attendee Capacity |
| L | attendeeCount | Public / 公开 | view / 只读 | uint256 / 无符号整数 | 当前参与人数 / Current Attendee Count |
| L | isEventActive | Public / 公开 | view / 只读 | bool / 布尔值 | 活动是否激活 / Whether Event is Active |
| L | hasAttended | Public / 公开 | view / 只读 | mapping(address ⇒ bool) / 映射 | 参与记录映射表 / Attendance Record Mapping |

### **3. 函数接口表 (Function Interfaces Table)**

| **L** | **Function Name / 函数名** | **Visibility / 可见性** | **Mutability / 可变性** | **Modifiers / 修饰器** | **Description / 描述** |
| --- | --- | --- | --- | --- | --- |
| L | constructor | Internal / 内部 | state-changing / 状态修改 | NO / 无 | 合约构造函数 / Contract Constructor |
| L | checkInWithSignature | Public / 公开 | state-changing / 状态修改 | eventActive | 单用户签名验证签到 / Single User Signature Check-in |
| L | batchCheckIn | Public / 公开 | state-changing / 状态修改 | eventActive | 批量签名验证签到 / Batch Signature Check-in |
| L | verifySignature | Public / 公开 | view / 只读 | NO / 无 | 验证签名有效性 / Verify Signature Validity |
| L | getMessageHash | Public / 公开 | view / 只读 | NO / 无 | 获取消息哈希值 / Get Message Hash for Signing |
| L | toggleEventStatus | Public / 公开 | state-changing / 状态修改 | onlyOrganizer | 切换活动状态 / Toggle Event Status |
| L | getEventInfo | Public / 公开 | view / 只读 | NO / 无 | 获取活动完整信息 / Get Complete Event Information |

**补充说明：**

**修饰器表 (Modifiers Table)**

| **L** | **Modifier Name / 修饰器名** | **Description / 描述** |
| --- | --- | --- |
| L | onlyOrganizer | 仅组织者可调用 / Only Organizer Can Call |
| L | eventActive | 仅活动激活状态可调用 / Only When Event is Active |

**事件日志表 (Event Logs Table)**

| **L** | **Event Name / 事件名** | **Parameters / 参数** | **Description / 描述** |
| --- | --- | --- | --- |
| L | EventCreated | (string name, uint256 date, uint256 maxAttendees) | 活动创建事件 / Event Created |
| L | AttendeeCheckedIn | (address attendee, uint256 timestamp) | 参与者签到事件 / Attendee Checked In |
| L | EventStatusChanged | (bool isActive) | 活动状态变更事件 / Event Status Changed |

**关键术语说明 (Key Terminology Explanation)**

| **Term / 术语** | **Meaning / 含义** | **Description / 描述** |
| --- | --- | --- |
| **Visibility / 可见性** | 访问权限控制 | 定义谁可以调用函数或访问变量 / Defines who can call functions or access variables |
| **Mutability / 可变性** | 状态修改能力 | view: 只读不修改状态; state-changing: 修改状态需要Gas费 / view: read-only; state-changing: modifies state and requires Gas |
| **Modifiers / 修饰器** | 函数修饰条件 | 在函数执行前检查的条件 / Conditions checked before function execution |
| **Public / 公开** | 完全公开访问 | 任何账户都可以调用 / Can be called by any account |
| **Internal / 内部** | 内部访问 | 仅合约内部或其他继承合约可调用 / Only within contract or inherited contracts |
| **State-changing / 状态修改** | 修改区块链状态 | 需要支付Gas费用，会产生交易 / Requires Gas fee and creates a transaction |
| **View / 只读** | 仅读取状态 | 免费调用，不修改状态 / Free to call, does not modify state |

**数据类型说明 (Data Types Explanation)**

| **Data Type / 数据类型** | **Description / 描述** | **Example / 示例** |
| --- | --- | --- |
| string / 字符串 | 文本数据类型 / Text data type | "Web3 Conference 2024" |
| address / 地址 | 以太坊账户地址 / Ethereum account address | 0x742d35Cc6634C0532925a3b8D0C9964E5Bd4f071 |
| uint256 / 无符号整数 | 256位正整数 / 256-bit positive integer | 1735660800 (时间戳) |
| bool / 布尔值 | 真/假值 / True/False value | true / false |
| mapping / 映射 | 键值对存储 / Key-value storage | address → bool (地址到布尔值的映射) |

# 2、细节详解

### **核心概念**

**（1）数字签名原理**：数字签名基于椭圆曲线密码学，提供身份验证和消息完整性：

**🔹 签名过程**

1. 用户使用私钥对消息哈希进行签名
2. 生成签名 (r, s, v) 三个参数
3. 将签名和消息一起发送

**🔹 验证过程**

1. 合约接收消息和签名
2. 使用ecrecover恢复签名者地址
3. 验证地址是否有权限

**优势：**无需在链上存储白名单，节省Gas费用

**🔍（2） ecrecover 函数**

`ecrecover`是Solidity内置函数，用于从签名恢复签名者地址：

```solidity
function ecrecover(bytes32 hash, uint8 v, bytes32 r, bytes32 s)
    returns (address signer)
```

**参数说明：**

- `hash`: 消息的keccak256哈希
- `v`: 恢复ID (通常是27或28)
- `r, s`: 签名的两个组成部分

```solidity
// 使用示例
bytes32 messageHash = keccak256(abi.encodePacked(user, eventId));
bytes32 ethSignedMessageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", messageHash));
address signer = ecrecover(ethSignedMessageHash, v, r, s);
```

📝 （3）**以太坊签名消息格式**

以太坊钱包签名时会自动添加前缀，防止签名重放攻击：

```solidity
// 原始消息哈希
bytes32 messageHash = keccak256(abi.encodePacked(data));

// 以太坊签名消息哈希
bytes32 ethSignedMessageHash = keccak256(abi.encodePacked(
    "\x19Ethereum Signed Message:\n32",
    messageHash
));
```

**前缀作用：**

- 防止用户误签交易数据
- 区分不同类型的签名
- 提高安全性

⚡ Gas优化策略

基于签名的访问控制相比传统白名单有显著的Gas优势：

**✅ 签名方式**

- 无需存储用户列表
- 验证成本固定 (~3000 gas)
- 支持无限用户
- 动态权限管理

**❌ 传统白名单**

- 每个用户需要存储 (~20000 gas)
- 查询成本随用户增加
- 修改白名单需要交易
- 存储成本高

### **分解代码**

### **拆解的合约：`EventEntry.sol`**

```jsx
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract EventEntry {
    string public eventName;
    address public organizer;
    uint256 public eventDate;
    uint256 public maxAttendees;
    uint256 public attendeeCount;
    bool public isEventActive;

    mapping(address => bool) public hasAttended;

    event EventCreated(string name, uint256 date, uint256 maxAttendees);
    event AttendeeCheckedIn(address attendee, uint256 timestamp);
    event EventStatusChanged(bool isActive);

    constructor(string memory _eventName, uint256 _eventDate_unix, uint256 _maxAttendees) {
        eventName = _eventName;
        eventDate = _eventDate_unix;
        maxAttendees = _maxAttendees;
        organizer = msg.sender;
        isEventActive = true;

        emit EventCreated(_eventName, _eventDate_unix, _maxAttendees);
    }

    modifier onlyOrganizer() {
        require(msg.sender == organizer, "Only the event organizer can call this function");
        _;
    }

    function setEventStatus(bool _isActive) external onlyOrganizer {
        isEventActive = _isActive;
        emit EventStatusChanged(_isActive);
    }

    function getMessageHash(address _attendee) public view returns (bytes32) {
        return keccak256(abi.encodePacked(address(this), eventName, _attendee));
    }

    function getEthSignedMessageHash(bytes32 _messageHash) public pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", _messageHash));
    }

    function verifySignature(address _attendee, bytes memory _signature) public view returns (bool) {
        bytes32 messageHash = getMessageHash(_attendee);
        bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);
        return recoverSigner(ethSignedMessageHash, _signature) == organizer;
    }

    function recoverSigner(bytes32 _ethSignedMessageHash, bytes memory _signature)
        public
        pure
        returns (address)
    {
        require(_signature.length == 65, "Invalid signature length");

        bytes32 r;
        bytes32 s;
        uint8 v;

        assembly {
            r := mload(add(_signature, 32))
            s := mload(add(_signature, 64))
            v := byte(0, mload(add(_signature, 96)))
        }

        if (v < 27) {
            v += 27;
        }

        require(v == 27 || v == 28, "Invalid signature 'v' value");

        return ecrecover(_ethSignedMessageHash, v, r, s);
    }

    function checkIn(bytes memory _signature) external {
        require(isEventActive, "Event is not active");
        require(block.timestamp <= eventDate + 1 days, "Event has ended");
        require(!hasAttended[msg.sender], "Attendee has already checked in");
        require(attendeeCount < maxAttendees, "Maximum attendees reached");
        require(verifySignature(msg.sender, _signature), "Invalid signature");

        hasAttended[msg.sender] = true;
        attendeeCount++;

        emit AttendeeCheckedIn(msg.sender, block.timestamp);
    }
}
```

该合同专为会议、研讨会或私人聚会等 Web3 活动而设计。但是，我们没有在链上存储一长串白名单地址（这需要 Gas 且不可扩展），而是做一些更聪明的事情。

诀窍如下：

 活动组织者为每个批准的与会者签署链下消息。

 然后，与会者将签名消息带到链上以**证明**他们已被邀请。

无需预先存储任何东西。智能合约使用 `ecrecover` 验证签名。

这是高效、安全的，并且反映了 IRL 门票或二维码的工作方式——但在链上。

### **（1）合同声明Contract Declaration**

```jsx
pragma solidity ^0.8.17;

contract EventEntry {
```

我们正在使用 Solidity 版本 0.8.17 并创建一个名为 EventEntry 的合约。简单的开始。

### **（2）事件详细信息和状态变量Event Details & State Variables**

在我们深入了解智能合约的核心逻辑之前，让我们退后一步问一个简单的问题：

Web3 事件实际上需要在链上管理什么？

我们不仅仅是在编写一个通用的出席跟踪器——我们正在为活动构建一个**基于签名、Gas 优化的私人访问系统** 。这意味着我们需要跟踪以下内容：

- 活动内容
- 谁负责
- 当它发生时
- 可参加人数
- 谁已经签到了
- 大门是否还开着

让我们看看所有这些是如何存储在合约中的。

```jsx
string public eventName;
address public organizer;
uint256 public eventDate;
uint256 public maxAttendees;
uint256 public attendeeCount;
bool public isEventActive;
```

这六个变量定义**了活动的一切** ——从谁负责到有多少人可以参加。让我们逐行逐句地浏览一下：

```jsx
string public eventName;
```

这是你活动的**人类可读名称human-readable name** ——例如 `“EthConf 2025”` 或 `“Token-Gated Summit”`。

由于它被标记为**公共public** ，Solidity 会自动创建一个 getter 函数。

任何人都可以调用 `eventName（）` 来获取此值——非常适合前端或浏览器显示事件的标题。

```jsx
address public organizer;
```

这是**事件组织者（** 部署合约的个人或实体）的以太坊地址。

只有这个地址才能**签署与会者批准** （链下）。

只有此地址可以**更改事件状态**  （`setEventStatus（）`）。

这使得组织者成为整个系统的看门人。

```jsx
uint256 public eventDate;
```

这保存了**事件的计划日期event's scheduled date** ，以 **Unix 时间戳**表示。

示例：`1714569600` → 2024 年 4 月 30 日 00：00：00 UTC

它用于 `checkIn（）` 函数，以确保人们不会签到太晚。

该合同允许在 `eventDate + 1 天`之前签到，以允许时区和延迟灵活性。

因此，在实践中，事件在此时间戳后一天**关闭** 。

```jsx
uint256 public maxAttendees;
```

这对可以签到的人数设定了**硬性上限hard cap** 。

如果设置为 `100`，则只有 100 个唯一地址可以成功签入。

对于管理私人活动中的有限座位、物理限制或访问控制非常有用。

```jsx
uint256 public attendeeCount;
```

这会保留已经签到的人数的**运行总数**  **running total**。

从 `0` 开始

每次新用户成功签入时递增 1

用于强制实施 `maxAttendees` 规则

```jsx
bool public isEventActive;
```

此变量确定事件**当前是否接受签入** 。

部署合约时设置为 `true`

可以由组织者使用 `setEventStatus（bool）` 打开/关闭

在事件处于非活动状态时阻止签入

### **(3)考勤跟踪Attendance Tracking**

```jsx
mapping(address => bool) public hasAttended;
```

我们使用此映射来**跟踪谁已经签到** ，因此没有人可以签到两次。

### ****

### **(4)事件Events**

[📢](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
event EventCreated(string name, uint256 date, uint256 maxAttendees);
event AttendeeCheckedIn(address attendee, uint256 timestamp);
event EventStatusChanged(bool isActive);
```

我们发出以下事件是为了提高透明度和前端集成：

`EventCreated`：在部署期间发出一次。

`AttendeeCheckedIn`：每次有人成功签到时触发。

`EventStatusChanged`：允许组织者暂停/恢复事件。

### ****

### **(5)构造函数：部署时设置 Constructor: Setup at Deployment**

[🏗️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
constructor(string memory _eventName, uint256 _eventDate_unix, uint256 _maxAttendees) {
    eventName = _eventName;
    eventDate = _eventDate_unix;
    maxAttendees = _maxAttendees;
    organizer = msg.sender;
    isEventActive = true;

    emit EventCreated(_eventName, _eventDate_unix, _maxAttendees);
}
```

`构造函数`就像安装向导，在部署合约时运行一次——而且只运行一次。让我们来看看每行的作用以及为什么它很重要：

```jsx
eventName = _eventName;
```

这会存储您的活动的人类可读名称（例如，“`Web3Conf 2025`”）。它作为构造函数参数传入并保存在链上，以便任何人都可以稍后使用 `eventName（）` 函数查询它。

```jsx
eventDate = _eventDate_unix;
```

这设置了官方活动日期——但它不是像“2025 年 4 月 21 日” 这样的格式化字符串。它是一个 **Unix 时间戳** （如 `1745251200`），这使得在 Solidity 中进行时间比较变得更加容易。

稍后，我们将使用它来检查事件是否仍在进行或已经结束。

```jsx
maxAttendees = _maxAttendees;
```

这为可以参加的人数设定了上限。例如，如果最大值为 150，则第 151 人将在入住时被拒绝。

拥有此内置限制有助于防止过度拥挤、垃圾邮件或滥用。

```jsx
organizer = msg.sender;
```

这会将部署合同的人员设置为**事件组织者** 。

`msg.sender` 是指部署合约的地址。

该地址具有特殊能力，例如激活/停用事件。

它也是**唯一**应该允许签署邀请签名的地址，我们稍后会验证。

```jsx
isEventActive = true;
```

默认情况下，活动以活动状态开始，这意味着除非组织者手动禁用它，否则允许签到。

稍后我们将创建一个名为 `setEventStatus（）` 的函数，让组织者切换此标志。

`emit EventCreated(...)`

这行向区块链广播事件，表明该事件已创建。

为什么要发出事件？

它可以帮助链下应用程序（如前端或浏览器）知道已经注册了新事件。

它记录有用的元数据，例如名称、日期和最大容量。

### **（6）存取控制Access Control**

```jsx
modifier onlyOrganizer() {
    require(msg.sender == organizer, "Only the event organizer can call this function");
    _;
}
```

保护某些功能的便捷**修饰符** 。只有组织者可以呼叫他们。

### **（7） 切换事件状态Toggle Event Status**

[🔁](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function setEventStatus(bool _isActive) external onlyOrganizer {
    isEventActive = _isActive;
    emit EventStatusChanged(_isActive);
}
```

使用此选项可以**暂停或恢复**签入。您可能希望在某个时间点之后冻结签到。

### ****

### **（8）消息散列 — 签名的关键Message Hashing — Key to Signatures**

[🔏](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function getMessageHash(address _attendee) public view returns (bytes32) {
    return keccak256(abi.encodePacked(address(this), eventName, _attendee));
}
```

此功能使**组织者**能够控制活动当前是否接受签到。

**它能做什么：**

[🧱](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

     **`onlyOrganizer` 修饰符：**

      确保只有部署合同的人员（ **组织者** ）才能更改此设置。这可以防止随机用户暂停或恢复事件。

**`isEventActive = _isActive;`**

            根据传递的参数更新事件的活动状态：

            如果 `_isActive` 为 `true`，则事件为打开。

            如果 `_isActive` 为 `false`，则签入将暂时暂停。

**`emit EventStatusChanged（_isActive）`;**

 每次状态变化时都会发出链上事件——对于前端仪表板或区块链浏览器立即反映变化很有用。

### **（9）以太坊签名消息哈希Ethereum Signed Message Hash**

[✍️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function getEthSignedMessageHash(bytes32 _messageHash) public pure returns (bytes32) {
    return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", _messageHash));
}
```

此功能是以太坊签名验证系统**的重要组成部分** ——这就是它存在的原因。

 ****

**为什么存在这个函数**

[🧠](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

     当用户在链下签署数据（如消息的哈希）时，从技术上讲，他们正在签署**任何随机的 32 字节** 。这可能包括交易的哈希值、合约的哈希值或一些完全不相关的数据。

这带来了风险：

     如果有人诱骗用户在链下签署某些内容，然后在链上重复使用该签名来做一些恶意的事情怎么办？

为了避免这种情况，以太坊引入了一个**保护性前缀** 。

### ****

### **（10）函数的作用**

[🔐](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这行：

```jsx
keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", _messageHash));
```

获取原始消息哈希并**用前缀包装它** ，如下所示：

```jsx
"\x19Ethereum Signed Message:\n32" + original_hash
```

然后它使用 `keccak256` 再次对整个事情进行哈希处理。

这被称为**以太坊签名消息哈希** ，它是像 MetaMask 这样的钱包在你调用 `eth_sign` 时使用的**确切格式** 。

 **为什么这很重要？**

[🔄](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

因为当我们稍后使用 `ecrecover（）` 恢复**签名者的地址**时，我们将从这个**前缀包装的哈希中**恢复它，而不是原始哈希。

如果您没有正确包装它，验证步骤将失败 - 即使用户正确签名！

### 

### **（11） 签名验证Signature Verification**

[🕵️‍♂️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
function verifySignature(address _attendee, bytes memory _signature) public view returns (bool) {
    bytes32 messageHash = getMessageHash(_attendee);
    bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);
    return recoverSigner(ethSignedMessageHash, _signature) == organizer;
}
```

此功能是我们**邀请验证系统**的核心。它回答了一个简单但有力的问题：

“这个签名真的是由组织者创建的吗——它是为这位与会者准备的吗？”

让我们逐行分解。

 **第 1 步：生成基本消息哈希**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
bytes32 messageHash = getMessageHash(_attendee);
```

这会重新创建**组织者**为特定与会者在链下签名的确切哈希值。

该哈希值通常类似于：

```jsx
keccak256(contract address + event name + attendee address)
```

这确保了：

签名与**此特定合约相关联**

仅对**此活动**有效

它属于**这个确切的用户**

如果有什么不同——不同的与会者、不同的活动——哈希值就会发生变化。

 **第 2 步：转换为以太坊签名格式**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);
```

正如我们之前所解释的，这用以太坊的标准前缀包装了消息哈希：

```jsx
"\x19Ethereum Signed Message:\n32" + messageHash
```

为什么？

因为像 MetaMask 这样的钱包在签名时会自动添加该前缀——所以我们在验证时也需要包含它，否则检查将失败。

 **第 3 步：恢复签名者**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
return recoverSigner(ethSignedMessageHash, _signature) == organizer;
```

在这里，我们：

使用 `ecrecover（）`（ 通过辅助函数 `recoverSigner`）提取签名**消息的地址** 。

将该地址与`组织者` （部署此合约的地址）进行比较。

如果匹配：  签名有效

如果没有：  有人伪造或由其他人签名

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

[❌](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

### **（12）recoverSigner – 密码侦探 The Cryptographic Detective**

[🔐](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这是合约中的实际函数：

```jsx
function recoverSigner(bytes32 _ethSignedMessageHash, bytes memory _signature)
    public
    pure
    returns (address)
{
    require(_signature.length == 65, "Invalid signature length");

    bytes32 r;
    bytes32 s;
    uint8 v;

    assembly {
        r := mload(add(_signature, 32))
        s := mload(add(_signature, 64))
        v := byte(0, mload(add(_signature, 96)))
    }

    if (v < 27) {
        v += 27;
    }

    require(v == 27 || v == 28, "Invalid signature 'v' value");

    return ecrecover(_ethSignedMessageHash, v, r, s);
}
```

此功能是我们基于签名的输入系统中的**最后一个侦探步骤** 。它查看签名并找出**哪个以太坊地址签署了它** 。

假设有人给了我们一个签名并声称：

“这是由活动组织者签署的。这意味着我被允许参加活动。

但我们不能盲目相信他们。我们需要**验证**是否：

该签名是有效的。

它真的来自**组织者的钱包** 。

这个函数可以帮助我们做到这一点。

 **第 1 步：检查签名长度**

[📏](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(_signature.length == 65, "Invalid signature length");
```

所有以太坊签名的**长度都是 65 字节** ——不多也不少。

如果它更短或更长，则可能是损坏的、不完整的或假的。

所以如果长度不对，我们会立即停止。

 **第 2 步：将签名分成 3 个部分**

[📦](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

以太坊签名不仅仅是一大块——它们由 3 个部分组成，称为：

`r`（32 字节）

`s`（32 字节）

`v`（1 字节）

这三个值协同工作，以**数学方式证明谁签署了邮件** 。

现在，事情变得有点技术性了......

 **第 3步：使用程序集提取这些值**

[🧙](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
assembly {
    r := mload(add(_signature, 32))
    s := mload(add(_signature, 64))
    v := byte(0, mload(add(_signature, 96)))
}
```

汇编是一种直接从内存访问数据的低级方法。

可以把它想象成挖一个盒子，准确地拿出我们需要的东西。

我们说：

“嘿以太坊，给我从位置 32 开始的前 32 个字节。那是 `r`。

“现在给我接下来的 32 字节，从 64 开始。那是`s` 。

“最后给我位置 96 的 1 字节。那是 `v`。

我们现在已经有了标志性拼图的所有部分。

 **第 4 步：根据需要修复 `V` 值**

[🧪](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
if (v < 27) {
    v += 27;
}
```

有时，不同的钱包或系统会给你一个 `v` 值，即 0 或 1。

但以太坊预计是 27 或 28。

所以我们只是在需要时进行调整。

 **第 5 步：验证 v 现在是否正确**

[🚨](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
require(v == 27 || v == 28, "Invalid signature 'v' value");
```

修复后，我们确保 v 是 27 或 28 - 其他任何东西都是不可接受的。

如果是其他任何东西，我们会抛出错误，因为我们不能信任签名。

 **第 6 步：恢复签名者的地址**

[🔍](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

```jsx
return ecrecover(_ethSignedMessageHash, v, r, s);
```

这是最后的时刻。

我们称之为 `ecrecover`——一个内置的以太坊函数，它采用：

签名消息哈希

签名值 （`v、r、s`）

它返回**签名者的地址** 。

yeah！我们现在知道是谁签署了这条消息。

### **(13)checkIn – Web3 活动的前门 The Front Gate of the Web3 Event**

[🎟️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

这是与会者到达您的活动时将调用的主要功能—— **无论是代币门控聚会、研讨会还是私人发布派对** 。

让我们先重新审视一下函数：

```jsx
function checkIn(bytes memory _signature) external {
    require(isEventActive, "Event is not active");
    require(block.timestamp <= eventDate + 1 days, "Event has ended");
    require(!hasAttended[msg.sender], "Attendee has already checked in");
    require(attendeeCount < maxAttendees, "Maximum attendees reached");
    require(verifySignature(msg.sender, _signature), "Invalid signature");

    hasAttended[msg.sender] = true;
    attendeeCount++;

    emit AttendeeCheckedIn(msg.sender, block.timestamp);
}
```

### **这个函数在做什么？**

[🧩](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

      此功能是**您的数字看门人** 。

 每次有人想要签到时，他们必须证明：

 他们被邀请（通过提供有效签名）

 他们在允许的窗口内签到

 活动仍在进行中

 他们尚未签到。

       还有空余！

如果他们通过了所有这些检查，他们就可以通过大门。

现在让我们逐行分解这个函数：

```jsx
require(isEventActive, "Event is not active");
```

在我们检查其他任何事情之前——该活动是否正在直播？

组织者可能已暂停或取消活动。如果是这样， **则不允许任何人签到** 。

此标志 （`isEventActive`） 由组织者使用 `setEventStatus（）` 函数进行控制。

```jsx
require(block.timestamp <= eventDate + 1 days, "Event has ended");
```

此检查上写着：“ **您只能在活动日期后 24 小时之前签到。**

为什么？

因为事件不会永远持续下去。您不希望有人在 5 天后尝试办理登check in手续。

通过增加一天，我们给予了轻微的宽限期，同时仍然保持现实。

```jsx
require(!hasAttended[msg.sender], "Attendee has already checked in");
```

我们不允许重复签到。

此行确保**每个地址只能签到一次** 。如果他们已被标记为有人值守，则他们将被阻止再次签到。

这是使用 `hasAttended` 映射进行跟踪的。

```jsx
require(attendeeCount < maxAttendees, "Maximum attendees reached");
```

这是你**活动上限** 。

如果你最多与会者人数为 100 人，并且已经有 100 人签到，那么无论谁试图进入，门都会关闭。

即使他们有有效的签名，一旦达到上限，他们也无法进入。

```jsx
require(verifySignature(msg.sender, _signature), "Invalid signature");
```

这是真正的魔力。

此行验证：

与会者 （`msg.sender`）  **实际上被邀请了**

他们的签名由**活动组织者签名**

它是专门**为这次活动签名**的

这使用了我们之前讨论的所有加密逻辑（消息哈希、以太坊前缀、`ecrecover`）——整齐地包装在 `verifySignature（）` 帮助程序中。

如果签名是假的或无效的，则拒绝访问。

### ****

**通过所有检查？太棒啦！**

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

如果与会者通过了所有要求，我们将记录他们的签到：

```jsx
hasAttended[msg.sender] = true;
```

我们将呼叫者标记为现在已签到的人。

这可以防止重复签入。

```jsx
attendeeCount++;
```

我们递增总与会者计数。

这有助于我们为下一个尝试进入的人强制执行 maxAttendees 限制。

```jsx
emit AttendeeCheckedIn(msg.sender, block.timestamp);
```

我们触发一个事件来记录此人已签到以及签到的时间。

这对以下情况很有用：

前端 UI

区块浏览器

链下数据索引（如 The Graph）

### 完整代码详解

### 📄 签名验证活动合约

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract SignThis {
    string public eventName;
    address public organizer;
    uint256 public eventDate;
    uint256 public maxAttendees;
    uint256 public attendeeCount;
    bool public isEventActive;

    mapping(address => bool) public hasAttended;

    event EventCreated(string name, uint256 date, uint256 maxAttendees);
    event AttendeeCheckedIn(address attendee, uint256 timestamp);
    event EventStatusChanged(bool isActive);

    constructor(string memory _eventName, uint256 _eventDate, uint256 _maxAttendees) {
        eventName = _eventName;
        organizer = msg.sender;
        eventDate = _eventDate;
        maxAttendees = _maxAttendees;
        isEventActive = true;

        emit EventCreated(_eventName, _eventDate, _maxAttendees);
    }

    modifier onlyOrganizer() {
        require(msg.sender == organizer, "Only organizer");
        _;
    }

    modifier eventActive() {
        require(isEventActive, "Event not active");
        _;
    }

    // 使用签名验证参与者身份
    function checkInWithSignature(
        address attendee,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external eventActive {
        require(attendeeCount < maxAttendees, "Event full");
        require(!hasAttended[attendee], "Already checked in");

        // 构造消息哈希
        bytes32 messageHash = keccak256(abi.encodePacked(
            attendee,
            address(this),  // 合约地址
            eventName
        ));

        // 以太坊签名消息哈希
        bytes32 ethSignedMessageHash = keccak256(abi.encodePacked(
            "\x19Ethereum Signed Message:\n32",
            messageHash
        ));

        // 恢复签名者地址
        address signer = ecrecover(ethSignedMessageHash, v, r, s);

        // 验证签名者是组织者
        require(signer == organizer, "Invalid signature");

        // 记录参与
        hasAttended[attendee] = true;
        attendeeCount++;

        emit AttendeeCheckedIn(attendee, block.timestamp);
    }

    // 批量签到 (Gas优化)
    function batchCheckIn(
        address[] calldata attendees,
        uint8[] calldata v,
        bytes32[] calldata r,
        bytes32[] calldata s
    ) external eventActive {
        require(attendees.length == v.length, "Array length mismatch");
        require(attendees.length == r.length, "Array length mismatch");
        require(attendees.length == s.length, "Array length mismatch");
        require(attendeeCount + attendees.length <= maxAttendees, "Would exceed capacity");

        for (uint256 i = 0; i < attendees.length; i++) {
            address attendee = attendees[i];

            if (hasAttended[attendee]) continue;  // 跳过已签到的

            bytes32 messageHash = keccak256(abi.encodePacked(
                attendee,
                address(this),
                eventName
            ));

            bytes32 ethSignedMessageHash = keccak256(abi.encodePacked(
                "\x19Ethereum Signed Message:\n32",
                messageHash
            ));

            address signer = ecrecover(ethSignedMessageHash, v[i], r[i], s[i]);

            if (signer == organizer) {
                hasAttended[attendee] = true;
                attendeeCount++;
                emit AttendeeCheckedIn(attendee, block.timestamp);
            }
        }
    }

    // 验证签名有效性 (不执行签到)
    function verifySignature(
        address attendee,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external view returns (bool) {
        bytes32 messageHash = keccak256(abi.encodePacked(
            attendee,
            address(this),
            eventName
        ));

        bytes32 ethSignedMessageHash = keccak256(abi.encodePacked(
            "\x19Ethereum Signed Message:\n32",
            messageHash
        ));

        address signer = ecrecover(ethSignedMessageHash, v, r, s);
        return signer == organizer;
    }

    // 获取消息哈希 (用于前端签名)
    function getMessageHash(address attendee) external view returns (bytes32) {
        return keccak256(abi.encodePacked(
            attendee,
            address(this),
            eventName
        ));
    }

    // 管理员功能
    function toggleEventStatus() external onlyOrganizer {
        isEventActive = !isEventActive;
        emit EventStatusChanged(isEventActive);
    }

    function getEventInfo() external view returns (
        string memory name,
        uint256 date,
        uint256 maxCapacity,
        uint256 currentCount,
        bool active
    ) {
        return (eventName, eventDate, maxAttendees, attendeeCount, isEventActive);
    }
}
```

**✅ 签名验证流程**

1. 组织者链下生成参与者签名 → 2. 参与者提交签名到合约 → 3. 合约使用ecrecover验证签名 → 4. 验证通过则允许签到。整个过程无需预先存储白名单！

### 🔍 前端签名生成示例

```jsx
// JavaScript 前端代码async function generateSignature(attendeeAddress, contractAddress, eventName) {
// 1. 构造消息const messageHash = ethers.utils.solidityKeccak256(
        ['address', 'address', 'string'],
        [attendeeAddress, contractAddress, eventName]
    );

// 2. 组织者签名const signature = await organizerWallet.signMessage(
        ethers.utils.arrayify(messageHash)
    );

// 3. 分离签名组件const { v, r, s } = ethers.utils.splitSignature(signature);

    return { v, r, s };
}

// 使用示例const signature = await generateSignature(
    "0x742d35Cc6634C0532925a3b8D0C9964E5Bd4f071",// 参与者地址
    contractAddress,
    "Web3 Conference 2024"
);

// 调用合约签到await contract.checkInWithSignature(
    attendeeAddress,
    signature.v,
    signature.r,
    signature.s
);
```

**⚠️ 安全注意事项**

- 签名可能被重放，需要添加nonce或时间戳
- 确保消息哈希包含足够的上下文信息
- 验证签名者身份的权威性
- 考虑签名的有效期限制
- 防止签名被用于其他合约

### 📝 Remix实战步骤

### 步骤1: 部署合约

1. 部署SignThis合约，设置活动信息
2. 记录合约地址和组织者地址

### 步骤2: 生成签名 (使用MetaMask)

1. 调用`getMessageHash(参与者地址)`获取消息哈希
2. 在浏览器控制台使用MetaMask签名：

```jsx
// 在浏览器控制台执行const messageHash = "0x...";// 从合约获取的哈希const signature = await ethereum.request({
    method: "personal_sign",
    params: [messageHash, ethereum.selectedAddress]
});
```

### 步骤3: 分离签名并验证

1. 使用在线工具或脚本分离签名的v, r, s
2. 调用`verifySignature`验证签名有效性
3. 调用`checkInWithSignature`完成签到

### 步骤4: 测试批量签到

1. 为多个地址生成签名
2. 调用`batchCheckIn`批量处理
3. 观察Gas消耗对比

### **😁如何在 Remix 中运行基于签名的事件输入系统**

好吧，是时候让我们的智能合约变为现实了。

你是组织者。i你即将举办 **Web3 峰会** 。让我们从头到尾来了解一下——包括签署邀请和签到人们！

### **第 1 步：部署合约**

[🔨](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

1.前往 Remix。

将你的 `EventEntry 合约`粘贴到一个新文件中 - 将其命名为 `EventEntry.sol`。

使用 **Solidity Compiler** 选项卡进行编译。

转到 **“Deploy & Run Transactions** ”选项卡。

现在是时候填写构造函数了：

`eventName`：“`Web3 峰会`”

`eventDate`：使用**future Unix timestamp**

→ 您可以使用类似 `Math.floor（Date.now（） / 1000） + 86400`（现在 + 1 天）之类的东西

→ 或者只是粘贴一个硬编码的未来时间戳，比如 `1714000000`

`最大与会者人数 ：100`

点击**Deploy。**

恭喜！您现在已经启动了智能活动。

### **第 2 步：生成消息哈希**

[🔑](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

要邀请客人，我们需要生成一个与他们的钱包绑定的唯一**哈希值** 。

在已部署的合约实例中：

叫：

```jsx
getMessageHash("0xAttendeeAddressHere")
```

 将 `“0xAttendeeAddressHere”` 替换为访客的钱包地址。

[🧠](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

复制返回的哈希值 — 我们将在下一步中使用它。

### **第 3 步：在 Remix 中签署消息**

[✍️](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

是时候充当**活动组织者**并签署客人的哈希了。

**创建一个 JavaScript 文件：**

在**文件资源管理器**中，右键单击并选择**新建文件**

命名 `sign.js`

粘贴此脚本：

```jsx
(async () => {
  const messageHash = "<paste-your-hash-here>";
  const accounts = await web3.eth.getAccounts();
  const organizer = accounts[0]; // first account in Remix
  const signature = await web3.eth.sign(messageHash, organizer);
  console.log("Signature:", signature);
})();
```

将 `“<paste-your-hash-here>”` 替换为您刚刚复制的哈希值。

**现在运行它：**

右键单击 `sign.js` 并选择**运行** 。

 Remix 会自动包含 **web3.js**，因此您无需安装任何东西。

[✅](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

您将在 Remix 终端中看到打印的签名。

**这是怎么回事？**

[🧠](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

我们使用部署者（即组织者）的**私钥**在链下**对消息哈希进行签名** 。这模拟了您的后端服务器在现实生活中会做什么。

### **第 4 步：以与会者身份签到**

[🪪](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

现在让我们切换角色 - 您是到达活动的与会者，因此切换到与会者地址

```jsx
checkIn("<paste-signature-here>")
```

 粘贴您从上一步获得的确切签名。

[📌](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

如果一切顺利：

系统将标记为已签到。

活动的与会人数将增加

协定将发出 `AttendeeCheckedIn` 事件 

[🎉](data:image/gif;base64,R0lGODlhAQABAIAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==)

就这样，我们建立了自己的 Web3 访客名单——无需在链上存储任何地址。我们没有弄乱存储空间或支付汽油来更新白名单，而是使用加密签名让与会者证明他们是被邀请的。活动组织者就像一个数字保镖，在幕后签署批准，而智能合约则在门口检查这些签名。它干净、高效且更加灵活——这是真实事件可以实际使用的那种系统。无论您是举办代币门控派对还是开发者聚会，这种模式都能为您提供所有安全性，而无需链上包袱。

# 3、下一步

**尝试以下改进:**

1. 添加nonce防止签名重放攻击
2. 实现签名有效期机制
3. 创建多级权限系统 (组织者、志愿者、参与者)
4. 实现签名撤销功能
5. 添加签名使用次数限制
6. 创建基于签名的投票系统

**扩展知识**

**EIP-712 结构化签名**

```solidity
// EIP-712 提供更安全的结构化签名
struct Permit {
    address owner;
    address spender;
    uint256 value;
    uint256 nonce;
    uint256 deadline;
}

bytes32 constant PERMIT_TYPEHASH = keccak256(
    "Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)"
);
```

EIP-712提供了更安全、用户友好的签名方式，被广泛用于DeFi协议。

**签名应用场景对比**

| 应用 | 优势 | 适用场景 |
| --- | --- | --- |
| 白名单验证 | Gas高效 | NFT铸造、活动签到 |
| 元交易 | 用户无需ETH | DApp用户体验 |
| Permit授权 | 一步完成授权 | DeFi协议集成 |