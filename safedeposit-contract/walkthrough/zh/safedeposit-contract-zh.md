# 模块化金库系统 SafeDeposit 合约

Day: Day 14
ID: 14
原文: https://builder-hub.notion.site/SafeDeposit-contract-1d55720a23ef80c1b2d0ebccd0fd594d?pvs=25
状态: 完成
译者: He Shadow
难度等级: 中级

[🧭 首页](https://www.notion.so/5-5-HerSolidity-28e06421268880e4b645d9458179e231?pvs=21) ｜ [🎓 30天课程日历](https://www.notion.so/28e0642126888002b26be4b2e9841ce0?pvs=21) ｜[](https://www.notion.so/28e06421268881e59a00e854a7444215?pvs=21) ｜[FAQ-Solidity答疑问题库](https://www.notion.so/2910642126888046a897d75705d86a58?pvs=21) ｜ [👩🏻‍💻 关于我们](https://www.notion.so/344d3328efef4b3ab742f92b61533ce8?pvs=21)

# **安全托管合约**

好的——到目前为止，我们已经编写了基础版和高级版的 ERC-20 代币，完成了完整的代币发售，并探讨了**继承**如何帮助我们重用和组织代码。回想一下我们的 TokenSale 合约是如何扩展 ERC-20 代币以添加新逻辑，而无需重写所有内容的。

现在我们要再次升级——这次我们将构建一个**模块化金库系统** ，让用户可以将敏感机密存储在不同类型的存款箱中。

---

## **🧠 核心理念**

想象一下：

你有很多用户，每个人都想把一个秘密放进自己的金库。不过，每个金库都不一样：

- 有些是**基础型**，适合日常使用。
- 有些是**高级型**，能提供额外的功能，比如元数据。
- 有些是**时间锁型**，用户在特定时间之前无法打开金库。

我们不需要写三个完全不同的合约，而是要**智能地构建它们**——使用一些共同的规则和逻辑以及 Solidity 强大的面向对象功能。

这就有一个很重要的概念，我们要先弄懂再继续：

---

## **📘 Solidity 中的接口是什么？**

如果说继承是用来**重复利用通用逻辑**，那么接口就是用来规定**强制执行规则**。

**接口**是一种只包含函数定义的合约——没有逻辑，没有存储，也没有状态变量。

你可以把接口想象成蓝图或者承诺：

> “任何实现此接口的合约都必须包含这些函数——并且它们的行为必须符合预期。”
> 

这就像定了一个标准，这样做有很多好处：

- ✅ 它使合约交互可预测——即使类型不一样也能互通
- ✅ 我们可以做一些工具（比如管理器或者仪表盘），只要合约用这个接口，就能和它沟通，不用管里面具体怎么写的
- ✅ 这样我们的系统就能像搭积木一样灵活组合

我们会用一个接口来规定每个金库必须要有的功能：

- 谁拥有这个金库？
- 金库能否存储和检索秘密？
- 这是什么类型的金库？
- 金库是什么时候创建的？

每种金库都会用自己的方式实现这个接口。

---

## **🧱 什么是抽象合约？**

除了接口，Solidity 还给了我们另一个强大的工具：**抽象合约**。

抽象合约是一个可以同时包含**已实现的函数**和**未实现的（virtual）函数**的合约——你可以把它想象成接口和真实逻辑的混合体。

当您想要执行以下操作时，它很有用：

- 提供通用逻辑（如通用变量或内部辅助函数）
- 有些具体的内容，可以让后面的“子合约”去补充完善。

关键思想是：

> 我们将抽象合约用作基础——不能直接用来部署。
> 

在我们的金库系统中，我们将构建一个抽象基础合约，处理通用逻辑，例如：

- 追踪金库所有者
- 存储和检索秘密
- 强制访问控制

然后我们的 `Basic`, `Premium`, 和`TimeLocked` 金库将**扩展**这个基础合约并添加自己的特色——同时都符合接口规范。

---

## **🛠️ 我们将要构建的内容**

我们将采取清晰易懂的步骤来解决这个问题：

### **1. `IDepositBox.sol` — 接口**

我们将定义一个所需函数的简单规则手册。每个金库都必须遵守此规则。

### **2. `BaseDepositBox.sol` — 抽象合约**

这是我们共享的基础。它会实现接口中定义的大部分逻辑，如秘密存储、所有权和存入时间。

它还会使用 `修饰符` 来限制访问——比如，确保只有所有者才可以访问或修改金库。

### **3. `BasicDepositBox.sol` — 标准金库**

这个金库只有接口规定的功能，它是默认的金库。

### **4. `PremiumDepositBox.sol` — 带附加功能的金库**

这个金库建立在基础金库上，但增加了对自定义元数据的支持。可以将其视为具有更多功能的升级版。

### **5. `TimeLockedDepositBox.sol` — 暂时无法打开的金库**

这个是有时效性的。在锁定期结束之前，无法查看秘密。

我们将添加特殊的修饰符来强制执行此行为。

### **6. `VaultManager.sol` — 控制器**

最后，我们将使用一个 `VaultManager` 合约将所有部分整合。

本合约：

- 允许用户创建任何类型的金库
- 跟踪哪些金库属于哪些用户
- 允许重命名金库
- 处理所有权转移
- 作为整个系统的单一交互点

---

## **为什么这很重要**

这个系统展示了如何编写可扩展、模块化且组织良好的 Solidity 代码——就像现实世界中的大规模协议一样。

它还教授了**接口**、**抽象合约**和**继承**如何协同工作——构成了大多数生产级智能合约的核心。

---

让我们从编写接口 `IDepositBox.sol` 开始深入。准备好了吗？我们开始建造金库吧。

## **`IDepositBox` — 我们的金库接口**

solidity

```
interface IDepositBox {
    function getOwner() external view returns (address);
    function transferOwnership(address newOwner) external;
    function storeSecret(string calldata secret) external;
    function getSecret() external view returns (string memory);
    function getBoxType() external pure returns (string memory);
    function getDepositTime() external view returns (uint256);
}

```

### **这里发生了什么？**

这是我们的**接口**——合约蓝图。

我们基本上是在说：“嘿，任何想要成为我们系统一部分的金库（或存款箱）**必须**实现这些函数。”

让我们看看每个函数代表什么：

- `getOwner()` — 返回存款箱的当前所有者。
- `transferOwnership()` — 允许将所有权转移给其他人。
- `storeSecret()` — 一个用于将字符串（我们的“秘密”）保存在金库中的函数。
- `getSecret()` — 检索存储的秘密。
- `getBoxType()` — 让我们知道它是哪种类型的存款箱（基础型、高级型等）。
- `getDepositTime()` — 返回存款箱的创建时间。

> 这些就像游戏规则一样——每种类型的存款箱都会遵循这些规则，即使它们的实施方式不同。
> 

## **`BaseDepositBox.sol` – 基础**型**抽象合约**

这个合约是我们存款箱系统的**核心**。所有特定类型的存款箱——如基础型、高级型和时间锁型——都建立在这个合约之上。

让我们从理解顶层结构开始。

json

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./IDepositBox.sol";

abstract contract BaseDepositBox is IDepositBox {
    address private owner;
    string private secret;
    uint256 private depositTime;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
    event SecretStored(address indexed owner);

    modifier onlyOwner() {
        require(msg.sender == owner, "Not the box owner");
        _;
    }

    constructor() {
        owner = msg.sender;
        depositTime = block.timestamp;
    }

    function getOwner() public view override returns (address) {
        return owner;
    }

    function transferOwnership(address newOwner) external virtual  override onlyOwner {
        require(newOwner != address(0), "New owner cannot be zero address");
        emit OwnershipTransferred(owner, newOwner);
        owner = newOwner;
    }

    function storeSecret(string calldata _secret) external virtual override onlyOwner {
        secret = _secret;
        emit SecretStored(msg.sender);
    }

    function getSecret() public view virtual override onlyOwner returns (string memory) {
        return secret;
    }

    function getDepositTime() external view virtual  override returns (uint256) {
        return depositTime;
    }
}

```

### **导入语句**

solidity

```
import "./IDepositBox.sol";

```

我们正在导入一个名为 `IDepositBox` 的接口。可以将接口视为一个**仅包含函数声明而没有实际实现的合约**。当我们导入并继承这个接口时，就是在说：

> “我承诺实现 IDepositBox 中声明的所有函数——即使不是所有函数都在这里实现。”
> 

这样做的好处是，每个存款箱都必须按照同样的标准来实现功能，就像大家都在用同一本说明书，这样结构就不会乱。

---

### **抽象合约**

solidity

```

abstract contract BaseDepositBox is IDepositBox

```

关键字 `abstract` 表示这个合约**不能直接部署**。它是充当其他合约构建的**模板**或**地基**。

为什么它是抽象的？

因为它没有把接口里规定的所有功能都写完整。例如，它**没有定义** `getBoxType()` 函数——每个后面的子合约会有自己的版本（如“基础型”、“高级型”等）。

所以这个基础合约只处理**通用逻辑**，剩下的细节由每个具体的金库自己补充。

---

## **🧱 状态变量**

solidity

```
address private owner;
string private secret;
uint256 private depositTime;

```

让我们分解一下：

- `owner`：存储拥有此存款箱人员的地址。只有此人被允许存储或检索秘密。
- `secret`：用户可以安全地存储在该存款箱中的私有字符串。
- `depositTime`：记录存款箱部署的准确时间（Unix 时间戳）。这对于基于时间的逻辑（例如，锁定）很有用。

这些变量都是 `private`，表示它们只能在内部访问。如果有人想读取它们，必须通过我们提供的**公共getter 函数**来查。

---

## **📣 事件**

solidity

```
event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
event SecretStored(address indexed owner);

```

事件有助于记录重要的链上活动。这些对于前端和像 Etherscan 或 TheGraph 等工具有用。

- `OwnershipTransferred`：当有人转移存款箱的所有权时触发。
- `SecretStored`：当存储新秘密时触发。

关键词 `indexed` 在查询链上数据时，可以更轻松地按这些字段过滤日志。

---

## **🔐 修饰符：`onlyOwner`**

solidity

```
modifier onlyOwner() {
    require(msg.sender == owner, "Not the box owner");
    _;
}

```

此修饰符限制对某些函数的访问。如果一个函数标记为 `onlyOwner`，那么只有当前所有者可以运行它。

否则，函数调用会回滚，并显示消息 `"Not the box owner"`。

我们在每个重要函数中使用这个修饰符——比如存储机密或转移所有权。

---

## **🧰 建造者**

solidity

```
constructor() {
    owner = msg.sender;
    depositTime = block.timestamp;
}

```

该函数只运行一次——在金库部署时。

- `msg.sender`：部署合约的人成为 `owner`。
- `block.timestamp`：当前时间（自 Unix 纪元以来的秒数）被记录为存入时间。

所以，该金库在创建时自动设置所有权和时间跟踪。

---

## **👤 `getOwner()`**

solidity

```
function getOwner() public view override returns (address) {
    return owner;
}

```

返回金库的当前所有者。这是一个简单的 getter 函数——并且是 `IDepositBox` 接口所要求的。

---

## **🔄 `transferOwnership()`**

solidity

```
function transferOwnership(address newOwner) external virtual override onlyOwner {
    require(newOwner != address(0), "New owner cannot be zero address");
    emit OwnershipTransferred(owner, newOwner);
    owner = newOwner;
}

```

允许当前所有者将所有权移交给其他人。

- 检查新的所有者地址不是零地址（`0x000...0`）——否则将永远锁定金库。
- 触发事件来表示所有权变更。

`onlyOwner` 修饰符确保**只有当前所有者**可以转移所有权。

---

## **🧳 `storeSecret()`**

solidity

```
function storeSecret(string calldata _secret) external virtual override onlyOwner {
    secret = _secret;
    emit SecretStored(msg.sender);
}

```

这是所有者将字符串存储在金库中的方式。

- 它只接受来自当前所有者（`onlyOwner`）的调用。
- 将秘密字符串存储在私有变量 `secret` 中。
- 存储后触发一个事件。

我们在这里使用 `calldata`，因为在传递字符串参数时，它在 gas 上更便宜。

---

## **🔍 `getSecret()`**

solidity

```
function getSecret() public view virtual override onlyOwner returns (string memory) {
    return secret;
}

```

此函数允许所有者检索存储的秘密。

- 这是一个 `view` 函数，因此如果从外部（例如从前端）调用，不会消耗 gas。
- 标记为 `onlyOwner` 以确保隐私——其他任何人都无法看到秘密。

---

## **🕒 `getDepositTime()`**

solidity

```
function getDepositTime() external view virtual override returns (uint256) {
    return depositTime;
}

```

返回金库部署的时间。对于基于时间的功能很有用——例如，如果一个秘密只在若干天后才能被查看。

## **🧱 `BasicDepositBox.sol` — 最简单的扩展**

好的，既然我们已经用 `BaseDepositBox` 合约构建了坚实的基础，是时候开始创建特定类型的存款箱了。

我们从最简单的开始：`BasicDepositBox`。

我们先来看完整的代码：

solidity

```
// SPDX-License-Identifier: MITpragma solidity ^0.8.0;

import "./BaseDepositBox.sol";

contract BasicDepositBox is BaseDepositBox {
    function getBoxType() external pure override returns (string memory) {
        return "Basic";
    }
}

```

乍一看，这个合约看起来非常短——确实如此。但在后台，它非常强大，因为它继承了 `BaseDepositBox` 的所有内容。

---

## **🔄 继承自 `BaseDepositBox`**

solidity

```
import "./BaseDepositBox.sol";

```

我们首先导入基础合约。这使 `BasicDepositBox` 能够访问基础合约中定义的**所有内容**：

- 所有权管理
- 秘密存储和检索
- 存入时间跟踪
- 访问控制修饰符
- 事件

然后我们像这样定义新合约：

solidity

```
contract BasicDepositBox is BaseDepositBox

```

这意味着：“创建一个新合约，**继承** `BaseDepositBox` 的所有逻辑。” 所以我们不需要重写所有那些逻辑——我们只需重复使用它。

这就是 Solidity 中继承的力量：你将核心功能在父合约中编写一次，然后在子合约中根据需要扩展它们。

---

## **🏷️ `getBoxType()` — 识别金库类型**

solidity

```
function getBoxType() external pure override returns (string memory) {
    return "Basic";
}

```

此合约定义的唯一函数，它是为了履行对 `IDepositBox` 接口的承诺。

让我们分解一下：

- `external`：此函数仅用于从合约外部调用（例如，从另一个合约或前端）。
- `pure`：它不读取或写入任何存储——它只是返回一个硬编码的字符串。
- `override`：它（当前函数）正在重写在 `IDepositBox` 中声明的**抽象** `getBoxType()` 函数（并且该函数在 `BaseDepositBox` 中未被实现）。
- `returns (string memory)`：它返回金库类型——在这种情况下是 `"Basic"`。

为什么有用？

假设你部署了许多金库——基础型、高级型和时间锁型——并且你想知道每个金库是什么类型。你可以简单地调用 `getBoxType()` 并获取一个人可读的标签。

这在构建以下内容时特别有用：

- 显示你金库的前端仪表板
- `VaultManager` 合约中的排序逻辑
- 链上分析

---

## **🧠 这个合约实际做什么**

虽然只有几行代码，`BasicDepositBox` 也为我们提供了一个功能齐全、安全、由所有者控制的存款箱。

它可以：

- 存储一个秘密字符串（只有所有者可以设置或检索它）
- 将所有权转移给其他人
- 在存储秘密或更改所有者时触发事件
- 将其自身金库类型报告为 `"Basic"`

所有这些能力都来自**基础合约**——我们只是添加了一个特定的标签来识别这种类型的金库。

## **💎 `PremiumDepositBox.sol` — 具备额外的元数据能力**

这次，我们再次构建在 `BaseDepositBox` 之上——但现在我们给存款箱**一些额外的东西**：一个叫做 `metadata` 的数据片段。

可以将其视为一个个性化的标签、标记或信息字段，所有者可以设置它。它可以描述秘密的内容、应该在何时访问，或者任何你想要附加的注释。

这是完整的合约：

solidity

```
// SPDX-License-Identifier: MITpragma solidity ^0.8.0;

import "./BaseDepositBox.sol";

contract PremiumDepositBox is BaseDepositBox {
    string private metadata;

    event MetadataUpdated(address indexed owner);

    function getBoxType() external pure override returns (string memory) {
        return "Premium";
    }

    function setMetadata(string calldata _metadata) external onlyOwner {
        metadata = _metadata;
        emit MetadataUpdated(msg.sender);
    }

    function getMetadata() external view onlyOwner returns (string memory) {
        return metadata;
    }
}

```

现在让我们逐步讲解每个部分。

---

## **✅ 继承和导入**

solidity

```
import "./BaseDepositBox.sol";

contract PremiumDepositBox is BaseDepositBox {

```

和之前一样，我们从 `BaseDepositBox` 导入基础逻辑。这使我们能够访问：

- 所有权检查
- 秘密存储
- 事件记录
- 存入时间戳

我们通过声明 `PremiumDepositBox` 为 `BaseDepositBox` 的子类来**扩展**它。

这意味着我们可以**重用所有核心功能**，并只关注这个版本的不同之处——即元数据。

---

## **🧾 `metadata` — 一个新的私有字段**

solidity

```
string private metadata;

```

我们引入了一个新的状态变量，称为 `metadata`。

它被标记为 `private`，这意味着只有**此合约内**的函数可以读取或修改它。（我们将为所有者创建外部访问函数。）

这可以用于任何东西——比如一个标签如“备份密钥”，或者一个注释如“退休后访问”，等等。

---

## **📢 `MetadataUpdated` 事件**

solidity

```
event MetadataUpdated(address indexed owner);

```

就像当有人存储秘密或转移所有权时我们会发出事件一样，当有人更新元数据时我们也会执行同样的事情。

因为：

- 它有助于链下系统（前端、浏览器）跟踪变化
- 它提高了透明度
- 它使调试更容易

我们还将 `owner` 标记为 `indexed`，以便在日志中便于过滤或搜索。

---

## **🏷️ `getBoxType()` — 识别金库类型**

solidity

```
function getBoxType() external pure override returns (string memory) {
    return "Premium";
}

```

和之前一样——这告诉我们这是什么类型的存款箱。

它返回 `"Premium"`，这有助于其他合约或前端正确识别和标记它。

这是 `IDepositBox` 接口所要求的，并帮助我们区分不同的金库类型（基础型、高级型、时间锁型）。

---

## **✍️ `setMetadata()` — 所有者可以读取**

solidity

```
function setMetadata(string calldata _metadata) external onlyOwner {
    metadata = _metadata;
    emit MetadataUpdated(msg.sender);
}

```

让我们解析一下：

- `external`：只能从合约外部调用（不能从内部调用）
- `onlyOwner`：使用我们从 `BaseDepositBox` 继承的修饰符来限制访问——只有金库所有者可以更新元数据
- `string calldata _metadata`：新的元数据值作为参数传入
- `metadata = _metadata`：我们更新存储的值
- `emit MetadataUpdated(msg.sender)`：我们记录变更以确保透明度

这为所有者提供了一种可以将注释、类别或标签附加到他们的金库上的方法。

---

## **📬 `getMetadata()` — 所有者可以读取它**

solidity

```
function getMetadata() external view onlyOwner returns (string memory) {
    return metadata;
}

```

此函数允许所有者**检索**他们之前存储的元数据。

它是 `view`，因为它只是从存储中读取而不改变任何东西。

同样，它受到 `onlyOwner` 的保护——其他任何人都看不到你写的内容。

## **⏳ `TimeLockedDepositBox.sol` — 暂时无法打开的金库**

这个合约引入了一个转折：**你可以存储一个秘密，但在特定时间过去之前你无法检索它**。

这就像一个数字金库，在一段设定的时间内保持锁定——无论你多么想打开它，你都必须等待。

我们再次从我们信任的 `BaseDepositBox` 继承，但这次我们添加了**定时逻辑**来控制秘密何时变得可访问。

让我们先通读完整的合约，然后逐行解释。

### **✅ 完整合约**

solidity

```
// SPDX-License-Identifier: MITpragma solidity ^0.8.0;

import "./BaseDepositBox.sol";

contract TimeLockedDepositBox is BaseDepositBox {
    uint256 private unlockTime;

    constructor(uint256 lockDuration) {
        unlockTime = block.timestamp + lockDuration;
    }

    modifier timeUnlocked() {
        require(block.timestamp >= unlockTime, "Box is still time-locked");
        _;
    }

    function getBoxType() external pure override returns (string memory) {
        return "TimeLocked";
    }

    function getSecret() public view override onlyOwner timeUnlocked returns (string memory) {
        return super.getSecret();
    }

    function getUnlockTime() external view returns (uint256) {
        return unlockTime;
    }

    function getRemainingLockTime() external view returns (uint256) {
        if (block.timestamp >= unlockTime) return 0;
        return unlockTime - block.timestamp;
    }
}

```

---

## **🔄 继承和导入**

solidity

```
import "./BaseDepositBox.sol";

contract TimeLockedDepositBox is BaseDepositBox {

```

就像 `BasicDepositBox` 和 `PremiumDepositBox` 一样，我们从 `BaseDepositBox` 导入共享基础逻辑。

我们继承了所有核心功能：

- 所有者跟踪
- 秘密存储
- 存入时间
- 事件日志记录

我们还会在此基础上添加时间锁功能。

---

## **🧭 `unlockTime` — 锁定计时器**

solidity

```
uint256 private unlockTime;

```

此变量存储了秘密可访问时的**时间戳**。

它是 `private`，意味着没有人可以直接访问它——但我们会通过 getter 函数公开它。

---

## **🏗️ 构造函数 — 锁定事务**

solidity

```
constructor(uint256 lockDuration) {
    unlockTime = block.timestamp + lockDuration;
}

```

以下是发生的事情：

- 当合约部署时，构造函数会采用一个数字：`lockDuration`，以**秒**为单位。
- 它将这个持续时间加到当前时间戳（`block.timestamp`）上，并将其设置为 `unlockTime`。

所以如果你传入 `3600`（1 小时），秘密将从部署起锁定 1 小时。

这使得合约非常灵活——你可以将秘密锁定几分钟、几小时、几天或几年。

---

## **🔐 `timeUnlocked` 修饰符 — 访问守门员**

solidity

```
modifier timeUnlocked() {
    require(block.timestamp >= unlockTime, "Box is still time-locked");
    _;
}

```

这是一个**自定义修饰符**——就像 `onlyOwner` 一样——但它会检查当前时间是否已超过解锁时间。

如果没有，函数调用将被拒绝并报错。

我们会在任何应受时间锁保护的函数上使用这个修饰符。

---

## **📦 `getBoxType()`**

solidity

```
function getBoxType() external pure override returns (string memory) {
    return "TimeLocked";
}

```

就像其他存款箱类型一样，这有助于我们识别这是哪种存款箱。

它返回字符串 `"TimeLocked"` 并实现 `IDepositBox` 接口。

---

## **🔍 `getSecret()` — 但仅在锁定之后**

solidity

```
function getSecret() public view override onlyOwner timeUnlocked returns (string memory) {
    return super.getSecret();
}

```

这个函数重写了 `BaseDepositBox` 中的常规函数 `getSecret()` 。

但现在，它增加了**两个**访问检查：

1. `onlyOwner`：只有金库所有者可以查看秘密。
2. `timeUnlocked`：只有在解锁时间过去后。

然后它调用 `super.getSecret()` 从基础合约中检索实际秘密。

这种分层保持了代码的整洁，并避免了逻辑重复。

---

## **📅 `getUnlockTime()` — 何时解锁？**

solidity

```
function getUnlockTime() external view returns (uint256) {
    return unlockTime;
}

```

这是一个简单的 getter 函数，它可以告诉您盒子解锁的**确切时间戳** 。

对于前端和显示目的很有用。

---

## **⏱️ `getRemainingLockTime()` — 倒计时助手**

solidity

```
function getRemainingLockTime() external view returns (uint256) {
    if (block.timestamp >= unlockTime) return 0;
    return unlockTime - block.timestamp;
}

```

这是另一个对前端友好的助手。

- 如果当前时间已经超过了解锁时间，我们返回 `0`。
- 否则，我们从 `unlockTime` 中减去 `now` 的时间，并返回金库可打开所需的**剩余秒数**。

对于在你的 UI 中创建倒计时、计时器或可视化进度条非常有用。

## **🧱 VaultManager — 你的金库仪表板**

这个合约充当**控制中心**，供用户创建、命名、管理和与他们的存款箱交互。

可以将其视为你的**金库应用后端**：

- 它允许用户创建不同类型的存款箱（基础型、高级型、时间锁型）。
- 它跟踪哪个用户拥有哪个存款箱。
- 它强制执行所有权规则。
- 它提供命名和检索存款箱信息的辅助函数。

---

## **✅ 完整合约代码（供参考）**

solidity

```
// SPDX-License-Identifier: MITpragma solidity ^0.8.0;

import "./IDepositBox.sol";
import "./BasicDepositBox.sol";
import "./PremiumDepositBox.sol";
import "./TimeLockedDepositBox.sol";

contract VaultManager {
    mapping(address => address[]) private userDepositBoxes;
    mapping(address => string) private boxNames;

    event BoxCreated(address indexed owner, address indexed boxAddress, string boxType);
    event BoxNamed(address indexed boxAddress, string name);

    function createBasicBox() external returns (address) {
        BasicDepositBox box = new BasicDepositBox();
        userDepositBoxes[msg.sender].push(address(box));
        emit BoxCreated(msg.sender, address(box), "Basic");
        return address(box);
    }

    function createPremiumBox() external returns (address) {
        PremiumDepositBox box = new PremiumDepositBox();
        userDepositBoxes[msg.sender].push(address(box));
        emit BoxCreated(msg.sender, address(box), "Premium");
        return address(box);
    }

    function createTimeLockedBox(uint256 lockDuration) external returns (address) {
        TimeLockedDepositBox box = new TimeLockedDepositBox(lockDuration);
        userDepositBoxes[msg.sender].push(address(box));
        emit BoxCreated(msg.sender, address(box), "TimeLocked");
        return address(box);
    }

    function nameBox(address boxAddress, string calldata name) external {
        IDepositBox box = IDepositBox(boxAddress);
        require(box.getOwner() == msg.sender, "Not the box owner");

        boxNames[boxAddress] = name;
        emit BoxNamed(boxAddress, name);
    }

    function storeSecret(address boxAddress, string calldata secret) external {
        IDepositBox box = IDepositBox(boxAddress);
        require(box.getOwner() == msg.sender, "Not the box owner");

        box.storeSecret(secret);
    }

    function transferBoxOwnership(address boxAddress, address newOwner) external {
        IDepositBox box = IDepositBox(boxAddress);
        require(box.getOwner() == msg.sender, "Not the box owner");

        box.transferOwnership(newOwner);

        address[] storage boxes = userDepositBoxes[msg.sender];
        for (uint i = 0; i < boxes.length; i++) {
            if (boxes[i] == boxAddress) {
                boxes[i] = boxes[boxes.length - 1];
                boxes.pop();
                break;
            }
        }

        userDepositBoxes[newOwner].push(boxAddress);
    }

    function getUserBoxes(address user) external view returns (address[] memory) {
        return userDepositBoxes[user];
    }

    function getBoxName(address boxAddress) external view returns (string memory) {
        return boxNames[boxAddress];
    }

    function getBoxInfo(address boxAddress) external view returns (
        string memory boxType,
        address owner,
        uint256 depositTime,
        string memory name
    ) {
        IDepositBox box = IDepositBox(boxAddress);
        return (
            box.getBoxType(),
            box.getOwner(),
            box.getDepositTime(),
            boxNames[boxAddress]
        );
    }
}

```

## **🧩 导入**

solidity

```
import "./IDepositBox.sol";
import "./BasicDepositBox.sol";
import "./PremiumDepositBox.sol";
import "./TimeLockedDepositBox.sol";

```

- `IDepositBox`：我们用来以统一方式与所有类型存款箱交互的接口。
- 其他三个是具体实现（实际可部署的合约）。

**为什么要使用接口？**

因为它让我们以相同的方式处理每个存款箱，无论其具体类型如何。所有存款箱都遵循相同的规则（`getOwner()`、`storeSecret()` 等），所以当我们调用这些共享函数时，不需要在意存款箱是什么类型。

---

## **🧠 状态变量**

solidity

```
mapping(address => address[]) private userDepositBoxes;
mapping(address => string) private boxNames;

```

- `userDepositBoxes`：将用户的地址映射到其拥有的所有存款箱（作为合约地址）。
- `boxNames`：允许用户为每个邮箱分配自定义名称。按邮箱地址存储。

---

## **🔔 事件**

solidity

```
event BoxCreated(address indexed owner, address indexed boxAddress, string boxType);
event BoxNamed(address indexed boxAddress, string name);

```

- `BoxCreated`：每次用户创建新存款箱时触发。
- `BoxNamed`：当用户给他们的存款箱自定义名称时触发。

这些有助于前端和浏览器显示发生的操作。

---

## **🏗️ createBasicBox 创建基础**存款箱

solidity

```
function createBasicBox() external returns (address) {
    BasicDepositBox box = new BasicDepositBox();
    userDepositBoxes[msg.sender].push(address(box));
    emit BoxCreated(msg.sender, address(box), "Basic");
    return address(box);
}

```

### **分解：**

- `BasicDepositBox box = new BasicDepositBox();`：这行代码**部署一个新的 BasicDepositBox 合约**并将其地址存储在变量 `box` 中。
- `userDepositBoxes[msg.sender].push(...)`：将新存款箱添加到发送者拥有的存款箱列表中。
- 触发一个事件，以便 UI 可以跟踪此创建。
- 返回新存款箱的地址以便于访问。

🧠 *这就是用户为自己“铸造”一个新数字*存款箱*的方式。*

---

## **🏗️ `createPremiumBox()` — 适用于想要存储额外元数据的用户**

solidity

```
function createPremiumBox() external returns (address) {
    PremiumDepositBox box = new PremiumDepositBox();
    userDepositBoxes[msg.sender].push(address(box));
    emit BoxCreated(msg.sender, address(box), "Premium");
    return address(box);
}

```

### **这个函数起什么作用？**

允许用户创建一个新的 **PremiumDepositBox** —— 一种特殊类型的存款箱，通过 `setMetadata()` 函数支持额外数据。

让我们逐行讲解：

---

### **1. 部署新的 PremiumDepositBox**

solidity

```

PremiumDepositBox box = new PremiumDepositBox();

```

这行代码**创建了一个全新的合约** `PremiumDepositBox` 。后台是这样的：

- 继承自 `BaseDepositBox`，从而获得获得所有权、秘密存储、存入时间等信息。
- 添加一个名为 `metadata` 的新状态变量以便更新/检索。

当这行代码运行时，合约被**部署在链上**，调用 `createPremiumBox()` 的用户成为该存款箱的所有者（感谢构造函数 `BaseDepositBox` ）。

---

### **2. 将**存款箱**保存在用户的**存款箱**列表中**

solidity

```

userDepositBoxes[msg.sender].push(address(box));

```

在这里，我们：

- 使用 `address(box)` 将合约转换为其地址
- 将该地址添加到创建它的用户的 `userDepositBoxes` 映射中

这确保了当用户稍后想检索他们全部存款箱时，这个存款箱会出现。

---

### **3. 为前端和跟踪触发事件**

solidity

```
emit BoxCreated(msg.sender, address(box), "Premium");

```

这会触发一个 `BoxCreated` 事件，记录：

- 创建存款箱的用户
- 新合约的地址
- 字符串 `"Premium"`，便于 UI 知道它是哪种类型的存款箱

这有助于钱包、仪表盘和区块链浏览器展示相关活动，而无需手动索引链上数据。

---

### **4. 返回**存款箱**地址**

solidity

```
return address(box);

```

最后，我们返回新创建的存款箱的地址，以便前端（或调用代码）可以立即与其交互。

---

## **⏳ `createTimeLockedBox()` — 适用于你不想立即打开的金库**

solidity

```
function createTimeLockedBox(uint256 lockDuration) external returns (address) {
    TimeLockedDepositBox box = new TimeLockedDepositBox(lockDuration);
    userDepositBoxes[msg.sender].push(address(box));
    emit BoxCreated(msg.sender, address(box), "TimeLocked");
    return address(box);
}

```

这个函数允许用户创建一个**时间锁存款箱** —— 一个行为就基础金库一样的智能合约，**但有一个关键的区别**：存储在里面的秘密只有在经过一定时间后才能被访问。

---

### **让我们逐步来看：**

---

### **1. 接受锁定持续时间**

solidity

```
function createTimeLockedBox(uint256 lockDuration)

```

这个函数设定一个数字：`lockDuration` —— 代表金库应该被锁定的**秒数**。

- 例如，如果 `lockDuration = 3600`，金库将被锁定 **1 小时**。
- 在此期间，所有者可以**存储秘密**，但他们在锁定到期**之前无法查看它**。

这非常适合以下情况：

- 时间胶囊
- 延迟显示消息
- 未来的礼物或承诺

---

### **2. 部署 TimeLockedDepositBox**

solidity

```

TimeLockedDepositBox box = new TimeLockedDepositBox(lockDuration);

```

这行代码创建了一个新实例 `TimeLockedDepositBox` ，传递 `lockDuration`。

在 `TimeLockedDepositBox` 构造函数内部，这个锁定持续时间被加到当前区块时间戳上，如下所示：

solidity

```

unlockTime = block.timestamp + lockDuration;

```

因此存款箱会根据区块链时间跟踪**何时可以安全解锁**。

部署者（`msg.sender`）自动成为所有者——得益于继承自 `BaseDepositBox` 。

---

### **3. 将存款箱保存在用户账户下**

solidity

```

userDepositBoxes[msg.sender].push(address(box));

```

将新存款箱的地址添加到调用者在 `userDepositBoxes` 映射中存储的存款箱列表中。

- 这使得每个用户都可以维护一个由多个存款箱组成的个人“金库”**。**
- 合约跟踪谁拥有哪个存款箱，但不会在链上存储完整的用户数据。

---

### **4. 触发 `BoxCreated` 事件**

solidity

```
emit BoxCreated(msg.sender, address(box), "TimeLocked");

```

事件是 dApp 在不持续查询智能合约状态的情况下跟踪链上操作的关键部分。

这将记录新存款箱的创建，包括：

- 谁创建了它
- 它的合约地址
- 存款箱类型：`"TimeLocked"`

前端可以使用它在 UI 中显示“新的时间锁金库已创建”或将其显示在用户活动反馈中。

---

### **5. 返回地址**

solidity

```
return address(box);

```

最后，函数返回新部署合约的地址，以便调用者（或前端）可以立即开始与其交互。

---

## **🏷️ nameBox**

solidity

```
function nameBox(address boxAddress, string calldata name) external {
    IDepositBox box = IDepositBox(boxAddress);
    require(box.getOwner() == msg.sender, "Not the box owner");

    boxNames[boxAddress] = name;
    emit BoxNamed(boxAddress, name);
}

```

### **这里发生了什么？**

- 首先，我们**将通用地址转换为接口**：
    
    solidity
    
    ```
    
    IDepositBox box = IDepositBox(boxAddress);
    
    ```
    
    这让我们可以在存款箱上调用 `getOwner()`，而无需知道它是什么类型。
    
- 然后我们**检查所有权**：
    
    solidity
    
    ```
    require(box.getOwner() == msg.sender, "Not the box owner");
    
    ```
    
    这确保只有合法的所有者可以重命名存款箱。
    
- 最后，我们保存新名称并触发一个事件。

---

## **📝 storeSecret**

solidity

```
function storeSecret(address boxAddress, string calldata secret) external {
    IDepositBox box = IDepositBox(boxAddress);
    require(box.getOwner() == msg.sender, "Not the box owner");

    box.storeSecret(secret);
}

```

- 相同的所有权模式。
- 调用接口中的 `storeSecret()` 函数，此函数的具体逻辑由每类存款箱自行实现。
- 这里没有触发事件，因为存款箱本身会触发一个（`SecretStored`）。

---

## **🔄 `transferBoxOwnership()` — 移交存款箱**

solidity

```
function transferBoxOwnership(address boxAddress, address newOwner) external {
    IDepositBox box = IDepositBox(boxAddress);
    require(box.getOwner() == msg.sender, "Not the box owner");

    box.transferOwnership(newOwner);

// 从旧所有者处移除金库address[] storage boxes = userDepositBoxes[msg.sender];
    for (uint i = 0; i < boxes.length; i++) {
        if (boxes[i] == boxAddress) {
            boxes[i] = boxes[boxes.length - 1];
            boxes.pop();
            break;
        }
    }

// 将金库添加到新所有者
    userDepositBoxes[newOwner].push(boxAddress);
}

```

这个函数允许存款箱的当前所有者**将所有权转移给其他人**——就像把存款箱的钥匙交给某人一样。

它还确保 `VaultManager` 更新自己的记录以反映这一变化。

让我们逐步来看：

---

### **1. 接口转换和所有权检查**

solidity

```

IDepositBox box = IDepositBox(boxAddress);
require(box.getOwner() == msg.sender, "Not the box owner");

```

- 首先，我们获取 `boxAddress`（这只是区块链上的一个常规地址），并将其**转换**为 `IDepositBox` 接口类型。
    
    这就是这行代码的作用：
    
    solidity
    
    ```
    
    IDepositBox box = IDepositBox(boxAddress);
    
    ```
    
    现在，我们可以调用像 `getOwner()` 或 `transferOwnership()` 这样的函数——因为我们已经告诉 Solidity，*“嘿，相信我，这个合约实现了 `IDepositBox` 接口。”*
    
- 接下来，我们验证调用此函数的人是否是该存款箱的**当前所有者**：
    
    solidity
    
    ```
    require(box.getOwner() == msg.sender, "Not the box owner");
    
    ```
    

如果他们不是所有者，函数将回滚。禁止未经授权的转移。

---

### **2. 调用存款箱的 `transferOwnership()` 方法**

solidity

```

box.transferOwnership(newOwner);

```

这行代码会告知存储箱合约，让其更新自己的内部 `owner` 状态。

这很重要：

真正的数据所有权和业务逻辑归属于存储箱合约—**VaultManager 不是其所有者**—因此，这一步确保了权限变更在存储箱内部自主完成。

---

### **3. 更新 VaultManager 的映射**

在**存储箱合约内部**更改所有权之后，我们必须确保 `VaultManager` 的 `userDepositBoxes` 映射也反映这一点。

如果我们不更新此列表，我们的系统将在前端或 API 中显示错误的所有者。

---

### **3.1 从发送者列表中移除**存储箱

solidity

```
address[] storage boxes = userDepositBoxes[msg.sender];
for (uint i = 0; i < boxes.length; i++) {
    if (boxes[i] == boxAddress) {
        boxes[i] = boxes[boxes.length - 1];
        boxes.pop();
        break;
    }
}

```

- 我们获取发送者的存储箱列表。
- 我们循环查找正在被转移的那个。
- 一旦找到：
    - 我们**将它与数组中的最后一项交换** 。
    - 然后调用 `.pop()` 来删除最后一项。
- 这是一个经典的 Solidity 技巧，用于从数组中移除项目而不留空位（因为 Solidity 数组不支持 `.remove(index)`）。

---

### **3.2 将**存储箱**添加到新所有者的列表**

solidity

```

userDepositBoxes[newOwner].push(boxAddress);

```

随后，我们将该存储箱存入新所有者的个人存储箱数组中——这步操作在我们的注册表内完成了所有权的转移。

---

## **📬 `getUserBoxes()` — 查看用户拥有的所有**存储箱

solidity

```
function getUserBoxes(address user) external view returns (address[] memory) {
    return userDepositBoxes[user];
}

```

### **它的作用：**

这个函数仅返回属于特定用户的**存款箱地址列表**。

### **为什么重要：**

- 每次有人创建一个存款箱（通过 `createBasicBox()`、`createPremiumBox()` 或 `createTimeLockedBox()`），该新合约的地址就会存储在 `userDepositBoxes` 映射中。
- 这个函数允许你检索到**任何用户地址**所对应的存储箱列表。

### **它如何工作：**

- `userDepositBoxes[user]` 存储的该特定用户的动态地址数组。
- 由于它标记为 `view`，它是**只读的**——调用这个函数**不消耗任何 gas**。
- 它返回一个完整的数组，可以在前端用来列出用户的所有金库。

> 使用场景：非常适合创建列出用户存款箱的仪表板或个人资料页面。
> 

---

## **🔎 `getBoxName()` — 读取存款箱的自定义名称**

solidity

```
function getBoxName(address boxAddress) external view returns (string memory) {
    return boxNames[boxAddress];
}

```

### **它的作用：**

返回所有者为特定存款箱指定的**自定义名称**。

### **名称来源：**

- 此前，用户可以调用 `nameBox(address, string)` 给他们的存款箱贴上自定义标签。
- 该标签存储在 `boxNames` 映射中。

### **逻辑：**

- 需要 `boxAddress` 作为输入。
- 在 `boxNames` 映射中查找地址，并返回用户设置的任何字符串。
- 如果没有给出名称，Solidity 只返回**默认的空字符串 `""`**。

> 使用场景：对于改进 UI 非常有用——你可以显示有意义的名称，如“主金库”或“爸爸的储物柜”，而不是显示原始地址。
> 

---

## **🧾 `getBoxInfo()` — 一次调用获取完整信息**

solidity

```
function getBoxInfo(address boxAddress) external view returns (
    string memory boxType,
    address owner,
    uint256 depositTime,
    string memory name
) {
    IDepositBox box = IDepositBox(boxAddress);
    return (
        box.getBoxType(),
        box.getOwner(),
        box.getDepositTime(),
        boxNames[boxAddress]
    );
}

```

这是用于获取关于存款箱的每个重要元数据的**一体化辅助函数**，。

让我们分解一下：

---

### **1. 接口转换**

solidity

```

IDepositBox box = IDepositBox(boxAddress);

```

- 我们获取原始的存款箱地址，并**将其转换为 IDepositBox 接口**。
- 这让我们可以安全地调用`getBoxType()`、`getOwner()` 和 `getDepositTime()` 函数，而无需确切知道它是哪种类型的存款箱（基础型、高级型、时间锁型等）。
- 只要合约实现了该接口，这些调用就会起作用。

---

### **2. 返回所有关键细节**

solidity

```
return (
    box.getBoxType(),
    box.getOwner(),
    box.getDepositTime(),
    boxNames[boxAddress]
);

```

以下是每个部分提供的内容：

- `box.getBoxType()` — 调用子合约的实现，并返回如下字符串 ：`"Basic"`、`"Premium"` 或 `"TimeLocked"`。
- `box.getOwner()` — 返回存款箱的当前所有者。
- `box.getDepositTime()` — 返回存款箱部署时的区块时间戳。
- `boxNames[boxAddress]` — 从 `VaultManager` 内部的 `boxNames` 映射中提取自定义名称（如果有）。

---

### **为什么这个函数有用：**

你可以在**一次**调用中获得所有内容，而不是进行**四次单独调用**：

- ✅ 存款箱类型
- ✅ 所有者
- ✅ 创建时间
- ✅ 自定义名称

> 使用场景：构建一个表格或卡片 UI，通过一次调用显示每个用户存款箱的完整摘要——对于高效的前端渲染非常有用。
> 

## **✅ 总结 — 一个构建得当的模块化金库系统**

这就是**金库系统**的全部内容。

我们从一个简单的想法开始——让用户在链上安全地存储秘密——最终使用 Solidity 最强大的概念构建了一个**模块化**、**可扩展**且**结构良好**的智能合约系统：

- ✅ **接口** 为所有金库定义通用标准
- ✅ **抽象合约** 提供了可复用的逻辑，并确保实现的一致性
- ✅ **继承** 在共享的底层架构之上，构建了多种类型的金库（基础型、高级型、时间锁型）
- ✅ **管理器合约** 来控制金库的创建、命名和所有权

您刚刚构建的项目，精准反映了现实世界智能合约系统的核心架构。Compound、Aave 和 OpenZeppelin 等主流平台均采用与此同源的分层设计——通过**接口实现标准化**，借助**抽象基础合约达成代码复用**，并在其上构建**清晰模块化的业务合约**。

所以现在，你不仅理解了**继承的工作原理**，还看到了如何构建随着产品增长而扩展的大型系统结构。